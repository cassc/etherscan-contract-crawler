// SPDX-License-Identifier: MIT

pragma solidity ^0.8.4;

import "@openzeppelin/contracts-upgradeable/access/AccessControlEnumerableUpgradeable.sol";

import "../LaunchpegErrors.sol";
import "./PendingOwnableUpgradeable.sol";

abstract contract SafeAccessControlEnumerableUpgradeable is
    PendingOwnableUpgradeable,
    AccessControlEnumerableUpgradeable
{
    /**
     * @dev Modifier that checks that the role is not the `DEFAULT_ADMIN_ROLE`
     */
    modifier roleIsNotDefaultAdmin(bytes32 role) {
        if (role == DEFAULT_ADMIN_ROLE)
            revert SafeAccessControlEnumerableUpgradeable__RoleIsDefaultAdmin();
        _;
    }

    /**
     * @dev Modifier that checks that an account is the `owner` or has a specific role
     */
    modifier onlyOwnerOrRole(bytes32 role) {
        if (msg.sender != owner() && !hasRole(role, msg.sender))
            revert SafeAccessControlEnumerableUpgradeable__SenderMissingRoleAndIsNotOwner(
                role,
                msg.sender
            );
        _;
    }

    function __SafeAccessControlEnumerable_init() internal onlyInitializing {
        __PendingOwnable_init();
        __AccessControlEnumerable_init();

        __SafeAccessControlEnumerable_init_unchained();
    }

    function __SafeAccessControlEnumerable_init_unchained()
        internal
        onlyInitializing
    {}

    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override(PendingOwnableUpgradeable, AccessControlEnumerableUpgradeable)
        returns (bool)
    {
        return
            PendingOwnableUpgradeable.supportsInterface(interfaceId) ||
            AccessControlEnumerableUpgradeable.supportsInterface(interfaceId);
    }

    /**
     * @notice Grants `role` to `account`.
     * @dev If `account` had not been already granted `role`, emits a {RoleGranted} event.
     *
     * Requirements:
     *
     * - the caller must be the `owner` or have ``role``'s admin role.
     * - the role granted can't be `DEFAULT_ADMIN`
     *
     * @param role The role to grant
     * @param account The address of the account
     */
    function grantRole(bytes32 role, address account)
        public
        virtual
        override(AccessControlUpgradeable, IAccessControlUpgradeable)
        roleIsNotDefaultAdmin(role)
        onlyOwnerOrRole(getRoleAdmin(role))
    {
        _grantRole(role, account);
    }

    /**
     * @notice Revokes `role` from `account`.
     * @dev If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must be the `owner` or have ``role``'s admin role.
     * - the role revoked can't be `DEFAULT_ADMIN`
     *
     * @param role The role to revoke
     * @param account The address of the account
     */
    function revokeRole(bytes32 role, address account)
        public
        virtual
        override(AccessControlUpgradeable, IAccessControlUpgradeable)
        roleIsNotDefaultAdmin(role)
        onlyOwnerOrRole(getRoleAdmin(role))
    {
        _revokeRole(role, account);
    }

    /**
     * @notice Revokes `role` from the calling account.
     *
     * @dev Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     * - the role renounced can't be `DEFAULT_ADMIN`
     *
     * @param role The role to renounce
     * @param account The address of the account
     */
    function renounceRole(bytes32 role, address account)
        public
        virtual
        override(AccessControlUpgradeable, IAccessControlUpgradeable)
        roleIsNotDefaultAdmin(role)
    {
        super.renounceRole(role, account);
    }

    /**
     * @notice Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     * @dev This also transfer the `DEFAULT_ADMIN` role to the new owner
     * @param _newOwner The address of the new owner
     */
    function _transferOwnership(address _newOwner) internal virtual override {
        _revokeRole(DEFAULT_ADMIN_ROLE, owner());
        if (_newOwner != address(0)) _grantRole(DEFAULT_ADMIN_ROLE, _newOwner);

        super._transferOwnership(_newOwner);
    }
}