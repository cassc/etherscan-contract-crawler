//SPDX-License-Identifier: UNLICENSED

//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &&&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@&&&&&&&&&&&&&&&&&&&&& &%@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@&&&&&&&&&&&&&&&&&&&&&&&& &&&*@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@&&&&&&&&&&&&&&&&&&&&&&&&& &&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &&&*           &&&&&&&&&&& &&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@&                    &&&&&&& &&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@                         &&&&& &&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@                             &&&&&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@           &&&(         ,&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@       &&&&&&&&& &&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@     &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@    &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@   &&&&&&&&&&&&&&&&&&&&&&&&&&&&.&&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@   &&&&&&&&&&&&&&&&&&&&&&&&&&&&&& &&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@  &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&& &&&&&@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@,&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&% &&&&@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&  &&&& @@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@      &&       .&&&&&&&&&&&&&&&&&&&&& &&&&&#@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@      &&&       *&&&&&&&&&&&&&&&&&&& &&&&&@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@              &&&&&&&&&&&&&&&&&&&&&  &&&& @@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@       &&&&&&&&&&&&&&&&&&&&&&&&&&&&  &&& @@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@    &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@            &&&&&&&&&&&&&&&&&&&&&&&&&(@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@   @         &&&&&&&&&&&&&&&&&&&&&&&&*@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@             &&&&&&&&&&&&&&&&&&&&&&&&*@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@        &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@,       &&&&&&&&&&&&&&&&&&&&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@       &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@       &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@       &&&&&&&&&&&&&&&&&&&&&&&% &&&&&&&&&&%@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@#       &&&&&&&&&&&&&&&&&&&   &&&&&&&&&&&&&&&&#@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@      &&&&&&&&&&&&&       &&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@                  &&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@               &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@                &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@                 &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@                 &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@              &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&[emailÂ protected]@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@            &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@           &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@         &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&*@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@         &&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@#         &&&&&&&&&&&&&&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@          &&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@.             &&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@                   ,&(@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@                 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@#*(@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

pragma solidity ^0.8.15;

/// @title Megalith Token
/// @author MYRLN

import "solmate/auth/Owned.sol";
import "ERC721A/ERC721A.sol";
import "openzeppelin-contracts/contracts/utils/Strings.sol";

contract MGLTH is ERC721A, Owned {
    using Strings for uint256;

    string public baseURI;

    bool public broadcastActive;
    bool public prayActive;
    bool public sacrificeActive = true;
    bool public developerSacramentState;

    uint public vandalismFee = 10**16;

    mapping (address => bool) public addressSacrificed;
    mapping (address => uint) public payedAmount;

    event Broadcast(address indexed injector, uint indexed token, string indexed mediaHash);
    event Vandalize(address indexed injector, uint indexed token, string indexed mediaash);

    constructor()ERC721A("MEGALITH", "KEYS")Owned(msg.sender){}

    /// @notice Receive function which calls the sacrifice function
    receive() external payable {
        sacrifice();
    }

    /// @notice Sacrifice function which enables claim of tokens for sender and updates the sender's payed amount
    function sacrifice() public payable {
        require(sacrificeActive, "Sacrificing is not active");
        addressSacrificed[msg.sender] = true;
        payedAmount[msg.sender] += msg.value;
    }

    /// @notice Function callable by sacrificers to claim a single token once the sacrifice period is over
    function pray() external {
        require(prayActive, "Praying is not enabled");
        require(msg.sender == tx.origin, "Praying from contracts is not allowed");
        require(addressSacrificed[msg.sender], "You have not sacrificed for this prayer");
        _mint(msg.sender, 1);
        addressSacrificed[msg.sender] = false;
    }

    /// @notice Function callable by token owners to broadcast content to the megalith stream
    /// @param token The token to utilize for the broadcast
    /// @param mediaHash Location of data to broadcast
    function broadcast(uint token, string calldata mediaHash) external {
        require(broadcastActive, "Broadcast is not active");
        require(ownerOf(token) == msg.sender, "You are not the owner of this token");
        emit Broadcast(msg.sender, token, mediaHash);
    }

    /// @notice Function callable by vandal token owners to vandalize content from the megalith stream
    /// @param token The token to utilize for the broadcast
    /// @param mediaHash Location of data to vandalize
    function vandalize(uint token, string calldata mediaHash) external payable {
        require(msg.value >= vandalismFee, "Insufficient funds");
        require(broadcastActive, "Broadcast is not active");
        require(ownerOf(token) == msg.sender, "You are not the owner of this token");
        require(_isPrime(token), "This token is not prime");
        emit Vandalize(msg.sender, token, mediaHash);
    }

    /// @notice Function called by vandalize function to check if the token being used is prime
    /// @return True if the token is prime, false otherwise
    /// @param n The number to check if it is prime
    function _isPrime(uint n) private pure returns (bool) {
        if (n <= 1) {
            return false;
        }
        for (uint256 i = 2; i < n; i++) {
            if (n % i == 0){
                return false;
            }        
        }
        return true;
    }

    /// @notice Function called by owner to update the baseURI string variable
    /// @param newBaseURI The new base URI for the contract
    function updateBaseURI(string calldata newBaseURI) external onlyOwner {
        baseURI = newBaseURI;
    }

    /// @notice Function called by owner to flip the sacrificeActive boolean variable
    function flipSacrificeActive() external onlyOwner {
        sacrificeActive = !sacrificeActive;
    }

    /// @notice Function called by owner to flip the brodcaseActive boolean variable
    function flipBroadcastActive() external onlyOwner {
        broadcastActive = !broadcastActive;
    }

    /// @notice Function called by owner to flip the prayActive boolean variable
    function flipPrayActive() external onlyOwner {
        require(!sacrificeActive, "Sacrifice is active");
        prayActive = !prayActive;
    }

    /// @notice Function called by owner to update the vandalismFee uint variable
    /// @param newVandalismFee The new value for the vandalismFee uint variable
    function updateVandalismFee(uint newVandalismFee) external onlyOwner {
        vandalismFee = newVandalismFee;
    }

    /// @notice Function called by owner to withdraw all funds from the contract
    function withdraw() external onlyOwner {
        uint balance = address(this).balance;
        payable(msg.sender).transfer(balance);
    }

    function developerSacrament(uint quantity) external onlyOwner {
        require(!developerSacramentState, "Developer sacrament has already been performed");
        require(quantity <= 1111, "You can only mint up to 1,111 tokens");
        _mint(msg.sender, quantity);
        developerSacramentState = true;
    }

    /// @notice Function called by marketplaces to return tokenURI for a given token
    /// @param tokenId The token to get the tokenURI for
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "This token does not exist.");
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }

}