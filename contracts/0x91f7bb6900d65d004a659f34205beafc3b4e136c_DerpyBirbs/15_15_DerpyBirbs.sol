// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Burnable.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

/**

 ______   _______  ______    _______  __   __    _______  ___   ______    _______  _______ 
|      | |       ||    _ |  |       ||  | |  |  |  _    ||   | |    _ |  |  _    ||       |
|  _    ||    ___||   | ||  |    _  ||  |_|  |  | |_|   ||   | |   | ||  | |_|   ||  _____|
| | |   ||   |___ |   |_||_ |   |_| ||       |  |       ||   | |   |_||_ |       || |_____ 
| |_|   ||    ___||    __  ||    ___||_     _|  |  _   | |   | |    __  ||  _   | |_____  |
|       ||   |___ |   |  | ||   |      |   |    | |_|   ||   | |   |  | || |_|   | _____| |
|______| |_______||___|  |_||___|      |___|    |_______||___| |___|  |_||_______||_______|


By Process Smith & DMADDE Studios

*/

contract DerpyBirbs is Context, ERC721Enumerable, ERC721Burnable, Ownable {
    uint internal constant MAX_MINTS_PER_TRANSACTION = 16;
    uint internal constant TOKEN_LIMIT = 8192;

    uint private nonce = 0;
    uint[TOKEN_LIMIT] private indices;
    uint private numTokens = 0;
    
    uint256 internal _mintPrice = 0;
    
    address payable internal artTeam = payable(0xdEC5190D59Ed370Aeed30341DCda3a2b8888e9c2); 
    address payable internal fappablo = payable(0xb72e9541FE46384D6942291F7B11db6bBB7dA956);
    address payable internal astronio = payable(0x9cCF31738Efcd642ABbe39202F7BD78f1495B8A4);
    address payable internal devasto = payable(0x7AC742F86aBF7E53314eBe4Cc7C71a5aAa930f6b);

    string internal _baseTokenURI;
    bool internal saleStarted = false;
    bool internal URISet = false;
    uint internal devSupplyAwarded = 0;

    /**
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     * See {ERC721-tokenURI}.
     */
    constructor(string memory name, string memory symbol,string memory baseTokenURI) ERC721(name, symbol) {
         _baseTokenURI = baseTokenURI;
        //dont call awardDevs() here, initial supply here, too much gas for one transaction
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }   
    
    /**
    * Can only be called twice. Gives 64 total Birbs to devs for giveaways, marketing purposes and team members.
    * 28 of these will go to Treasure Chests holders.
    */
    function give32BirbsToDevs() external onlyOwner {
        require(saleStarted == false,"Sale has already started");
        require(devSupplyAwarded < 2,"Dev supply has already been awarded");
        uint i;
        uint id;

        for(i = 0; i < 32; i++){
            id = randomIndex();
            _mint(fappablo, id);
            numTokens = numTokens + 1;
        }

        devSupplyAwarded = devSupplyAwarded+1;
    } 

    /**
    * Credits to LarvaLabs Meebits contract
    */
    function randomIndex() internal returns (uint) {
        uint totalSize = TOKEN_LIMIT - numTokens;
        uint index = uint(keccak256(abi.encodePacked(nonce, msg.sender, block.difficulty, block.timestamp))) % totalSize;
        uint value = 0;

        if (indices[index] != 0) {
            value = indices[index];
        } else {
            value = index;
        }

        // Move last value to selected position
        if (indices[totalSize - 1] == 0) {
            // Array position not initialized, so use position
            indices[index] = totalSize - 1;
        } else {
            // Array position holds a value so use that
            indices[index] = indices[totalSize - 1];
        }
        nonce++;
        // Don't allow a zero index, start counting at 1
        return value+1;
    }
    
    /**
     * @dev Creates a new token. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     * 
     */
    function mint(address to, uint amount) external payable{
        //check sale start
        require(saleStarted == true, "Sale has not started yet");

        //only 8192 birbs
        require(numTokens < TOKEN_LIMIT, "No Birbs left in the nest");

        //mint at least one
        require(amount > 0, "Must mint at least one");

        //32 max per transaction
        require(amount <= MAX_MINTS_PER_TRANSACTION, "Trying to mint too many");

        //dont overmint
        require(amount <= TOKEN_LIMIT-numTokens,"Not enough Birbs left to mint");

        //check payment
        require(msg.value >= _mintPrice * amount, "msg.value too low");

        uint id;
        uint i;

        for(i = 0; i < amount; i++){
            id = randomIndex();
            _mint(to, id);
            numTokens = numTokens + 1;
        }

    }

    /**
     * @dev Withdraws funds to dev and art teams
     *
     */
    function withdrawFunds() external virtual {
        uint256 halfBalance = address(this).balance / 2;
        uint256 thirdOfHalfBalance = halfBalance / 3;
        artTeam.transfer(halfBalance);
        fappablo.transfer(thirdOfHalfBalance);
        astronio.transfer(thirdOfHalfBalance);
        devasto.transfer(thirdOfHalfBalance);
    }

    /**
    * Devs cant change URI after the sale begins 
    */
    function setBaseURI(string memory baseTokenURI) external onlyOwner {
        require(saleStarted == false,"Can't change metadata after the sale has started");
        _baseTokenURI = baseTokenURI;
        URISet = true;
    }

    /**
    * Start the sale (cant be stopped later)
    */
    function startSale(uint256 mintPrice) external virtual onlyOwner {
        require(saleStarted == false,"Sale has already started");
        require(URISet == true, "URI not set");
        require(mintPrice > 0.01 ether,"Price too low");
        _mintPrice = mintPrice;
        saleStarted = true;
    }

    function _beforeTokenTransfer(address from, address to, uint256 tokenId) internal virtual override(ERC721, ERC721Enumerable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC721, ERC721Enumerable) returns (bool) {
        return super.supportsInterface(interfaceId);
    }

    function getAmountMinted() external view returns (uint256) {
        return numTokens;
    }

    function getMaxSupply() external pure returns (uint256) {
        return TOKEN_LIMIT;
    }

    function getMintPrice() external view returns (uint256) {
        return _mintPrice;
    }

    function hasSaleStarted() external view returns (bool) {
        return saleStarted;
    }

}