//SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.7;

import "./BalanceIncentivesBase.sol";

/// @notice `LockBalanceIncentives` allows for balance rewards to be locked up by the user for a
///         selected period of time.
///
///         Users receive portion of the locked function that is 1/(X^2), where X is the lock time
///         divided by the max lock time.  This way, locking for half of the max lock time would
///         give the user 1/4 of the locked balance.  And locking for the full max time will provide
///         all of the balance.
///
///         Excessive balance is transferred to the `treasury`.
abstract contract LockBalanceIncentives is BalanceIncentivesBase {
    using SafeERC20 for IERC20;

    struct Reward {
        uint256 amount;
        uint256 availTimestamp;
    }

    /// @notice The max lockup time which will yield 100% reward to users
    uint256 public maxLockupTime;

    /// @notice The address of the treasury of the FST protocol
    address public treasury;

    /// @notice A mapping of rewards mapped by user address and checkoutId
    ///         The newest checkout id can be obtained from requestIdByAddress
    mapping(address => mapping(uint256 => Reward)) rewardsByAddressById;
    /// @notice The latest unused checkout id for an address
    mapping(address => uint256) public requestIdByAddress;

    function initializeLockBalanceIncentives(address _treasury, address _rewardsToken)
        internal
        initializer
    {
        maxLockupTime = 52 weeks;
        treasury = nonNull(_treasury);
        BalanceIncentivesBase.initializeBalanceIncentivesBase(_rewardsToken);
    }

    /// @notice Claim rewards token for a given account
    function claim() external {
        super.doClaim(msg.sender, maxLockupTime);
    }

    /// @notice Claim rewards token for a given account
    /// @param lockupTime the time to lockup tokens
    function claimWithLockupTime(uint256 lockupTime) external {
        super.doClaim(msg.sender, lockupTime);
    }

    /// @dev Customizes the base class `claim()` behaviour.
    ///
    ///      If this contract specifies non-zero `maxLockupTime`, we are going to lock the tokens for
    ///      the `lockupTime` seconds, instead of transferring them to the `account` right away.  The
    ///      user will need to call `claim()` after `lockupTime` seconds to get the tokens.
    ///
    ///      With zero `maxLockupTime` we send the tokens to `account` immediately.
    function sendTokens(
        address account,
        uint256 amount,
        uint256 lockupTime
    ) internal override {
        // If there is no lockup time send the tokens directly to the user
        if (maxLockupTime == 0) {
            super.sendTokens(account, amount, lockupTime);
            return;
        }

        if (lockupTime > maxLockupTime) {
            lockupTime = maxLockupTime;
        }

        uint256 base = (lockupTime * 1 ether) / maxLockupTime;
        uint256 squared = (base * base) / 1 ether;
        uint256 tokensReceived = (squared * amount) / 1 ether;
        uint256 tokensForfeited = amount - tokensReceived;

        require(tokensReceived > 0, "no tokens");

        uint256 time = getTime() + lockupTime;
        uint256 requestId = requestIdByAddress[account]++;

        rewardsByAddressById[account][requestId] = Reward(tokensReceived, time);

        emit CheckoutRequest(account, requestId, tokensReceived, time, tokensForfeited);

        // We control the rewardsToken so there is no reentrancy attack, but we
        // defensively order the transfer last anyway.
        if (tokensForfeited > 0) {
            rewardsToken.safeTransfer(treasury, tokensForfeited);
        }
    }

    /// @notice Update the max lock up time for rewards tokens
    ///         Note setting a time of zero disables lockup all together and the contract
    ///         directly sends tokens to users on claim
    /// @param _maxLockupTime The new time
    function setMaxLockupTime(uint256 _maxLockupTime) external onlyOwner {
        emit MaxLockupTimeChange(maxLockupTime, _maxLockupTime);
        maxLockupTime = _maxLockupTime;
    }

    /// @notice Returns rewards for a given account and rewardsId
    /// @param _account The account to look up
    /// @param _rewardsId The rewards id to look up
    function getRewards(address _account, uint256 _rewardsId)
        external
        view
        returns (uint256 amount, uint256 availableTimestamp)
    {
        Reward memory reward = rewardsByAddressById[_account][_rewardsId];
        amount = reward.amount;
        availableTimestamp = reward.availTimestamp;
    }

    /// @notice Checkout rewards token for a given account
    /// @param _account the account to claim for
    /// @param _rewardsId index of the claim made
    function checkout(address _account, uint256 _rewardsId) external {
        Reward storage reward = rewardsByAddressById[_account][_rewardsId];

        require(reward.amount > 0, "no reward");

        require(getTime() >= reward.availTimestamp, "Not available yet");

        uint256 amount = reward.amount;
        reward.amount = 0;

        emit Checkout(_account, _rewardsId, amount);

        // Note: Ordering in this method matters:
        // We need to ensure that balances are deducted from storage variables
        // before calling the transfer function since we potentially would
        // be susceptible to a reentrancy attack pulling out funds multiple times.
        rewardsToken.safeTransfer(_account, amount);
    }

    /// @notice Emitted when a user claims lock up tokens
    /// @param account The account claiming tokens
    /// @param requestId The request id for the account
    /// @param amount The amount of tokens being claimed
    /// @param availTimestamp when the tokens will be available for checkout
    /// @param tokensForfeited Tokens that have been sent to the treasury
    event CheckoutRequest(
        address indexed account,
        uint256 requestId,
        uint256 amount,
        uint256 availTimestamp,
        uint256 tokensForfeited
    );

    /// @notice Emitted when a user checkouts their locked tokens
    /// @param account The account claiming the tokens
    /// @param requestId The request id for the account
    /// @param amount The amount of tokens being claimed
    event Checkout(address indexed account, uint256 requestId, uint256 amount);

    /// @notice Emitted when maxLockupTime is changed
    /// @param oldMaxLockupTime The old lockup time
    /// @param newMaxLockupTime The new lockup time
    event MaxLockupTimeChange(uint256 oldMaxLockupTime, uint256 newMaxLockupTime);
}