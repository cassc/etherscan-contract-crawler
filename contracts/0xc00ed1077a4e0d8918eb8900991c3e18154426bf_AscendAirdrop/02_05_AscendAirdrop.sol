//SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.17;

import "../SafeERC20.sol";

/**
 * @notice Thank you to the original authors of this CA for their contributions to efficiency ASCEND has upgraded.
 * @notice Hyper efficient Airdrop contract for ERC20 and ETH
 * @author Harrison (@PopPunkOnChain)
 * @author Magna (@MagnaTokens)
 */
contract AscendAirdrop {
    /**
     *
     * @param _recipients list of recipients
     * @param _amounts  list of amounts to send each recipient
     */
    function AscendETH(
        address[] calldata _recipients,
        uint256[] calldata _amounts
    ) external payable {
        // looop through _recipients
        assembly {
            // store runningTotal
            let runningTotal := 0
            // store length of _recipients
            let sz := _amounts.length
            for {
                let i := 0
            } lt(i, sz) {
                // increment i
                i := add(i, 1)
            } {
                // store offset for _amounts[i]
                let offset := mul(i, 0x20)
                // store _amounts[i]
                let amt := calldataload(add(_amounts.offset, offset))
                // store _recipients[i]
                let recp := calldataload(add(_recipients.offset, offset))
                // send _amounts[i] to _recipients[i]
                let success := call(
                    gas(),
                    recp, // address
                    amt, // amount
                    0,
                    0,
                    0,
                    0
                )
                // revert if call fails
                if iszero(success) {
                    revert(0, 0)
                }
                // add _amounts[i] to runningTotal
                runningTotal := add(runningTotal, amt)
            }
            // revert if runningTotal != msg.value
            if iszero(eq(runningTotal, callvalue())) {
                revert(0, 0)
            }
        }
    }

    /**
     *
     * @param _token ERC20 token to airdrop
     * @param _recipients list of recipients
     * @param _amounts list of amounts to send each recipient
     * @param _total total amount to transfer from caller
     */
    function ascendERC20(
        IERC20 _token,
        address[] calldata _recipients,
        uint256[] calldata _amounts,
        uint256 _total
    ) external {
        // bytes selector for transferFrom(address,address,uint256)
        bytes4 transferFrom = 0x23b872dd;
        // bytes selector for transfer(address,uint256)
        bytes4 transfer = 0xa9059cbb;

        assembly {
            // store transferFrom selector
            let transferFromData := add(0x20, mload(0x40))
            mstore(transferFromData, transferFrom)
            // store caller address
            mstore(add(transferFromData, 0x04), caller())
            // store address
            mstore(add(transferFromData, 0x24), address())
            // store _total
            mstore(add(transferFromData, 0x44), _total)
            // call transferFrom for _total
            let successTransferFrom := call(
                gas(),
                _token,
                0,
                transferFromData,
                0x64,
                0,
                0
            )
            // revert if call fails
            if iszero(successTransferFrom) {
                revert(0, 0)
            }

            // store transfer selector
            let transferData := add(0x20, mload(0x40))
            mstore(transferData, transfer)

            // store length of _recipients
            let sz := _amounts.length

            // loop through _recipients
            for {
                let i := 0
            } lt(i, sz) {
                // increment i
                i := add(i, 1)
            } {
                // store offset for _amounts[i]
                let offset := mul(i, 0x20)
                // store _amounts[i]
                let amt := calldataload(add(_amounts.offset, offset))
                // store _recipients[i]
                let recp := calldataload(add(_recipients.offset, offset))
                // store _recipients[i] in transferData
                mstore(
                    add(transferData, 0x04),
                    recp
                )
                // store _amounts[i] in transferData
                mstore(
                    add(transferData, 0x24),
                    amt
                )
                // call transfer for _amounts[i] to _recipients[i]
                let successTransfer := call(
                    gas(),
                    _token,
                    0,
                    transferData,
                    0x44,
                    0,
                    0
                )
                // revert if call fails
                if iszero(successTransfer) {
                    revert(0, 0)
                }  
            }
        }
    }
}