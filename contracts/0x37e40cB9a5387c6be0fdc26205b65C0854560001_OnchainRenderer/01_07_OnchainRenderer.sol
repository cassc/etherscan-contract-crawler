// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

/**===============================================================================
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,@@@@@@@@@@@@@@,,,,,,,,,@@@@@@@@@@@@@@,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,@@(((((,,,,,,,,,@@,,,,,@@((,,,,,,,,,,  @@,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,@@(((((,,,,,,,,,,,,,@@@@@((,,,,,,,,,,,,,,  @@,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,@@@((((,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,  @@@,,,,,,,,,,,,,,,
,,,,,,,,,,,,@@(((((((,,,,,,,,,,,,,,,,@@,,,,,,,,,,,,@@@@@@@@@@@,,,,,@@,,,,,,,,,,,,,
,,,,,,,,,,,,@@(((((((,,,,,,,,,,,,,,,,,,,,,,,,,,,@@@@@@@@@@    @@,,,@@,,,,,,,,,,,,,
,,,,,,,,,,@@(((((((,,,,@@@@@@@@@@@@@@,,@@@,,,,@@     @@@@@    @@,,,,,@@,,,,,,,,,,,
,,,,,,,,,,@@(((((((,,,,,,,,,,,,,,,,,,@@@@@,,,,@@   @@@@@@@    @@,,,,,@@,,,,,,,,,,,
,,,,,,,@@@(((((((((,,,,,,,,,,,,,,,,,,,,,,,,,,,,,@@@@@@@@@@@@@@,,,,,,,,,@@@,,,,,,,,
,,,,,,,@@@(((((((((@@@@@@@,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,@@@,,,,,,,,
,,,,,,,@@@((((((@@@(((((((@@@@,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,@@@@@@@,,,,@@@,,,,,,,,
,,,,,,,@@@((((((@@@((@@(((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((@@,,@@@,,,,,,,,
,,,,,,,@@@((((((@@@((((@@@@@((((((((((((((((((((((((((((((((@@@@(((@@,,@@@,,,,,,,,
,,,,,,,@@@(((((((((@@(((((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(((((((@@,,@@@,,,,,,,,
,,,,,,,@@@(((((((((**##############################################,,**@@@,,,,,,,,
,,,,,,,,,,@@(((((((((((,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,((@@,,,,,,,,,,,
,,,,,,,,,,,,@@(((((((((((((((((((((((((((((((((((((((((((((((((((((@@,,,,,,,,,,,,,
,,,,,,,,,,,,,,@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
==================================================================================
ğŸ¸                          ON-CHAIN RENDERING LOGIC                            ğŸ¸
ğŸ¸                       THE PEOPLES' NFT MADE BY FROGS                         ğŸ¸
================================================================================*/

/**
 ğŸ¸ @title OnChainRenderManager
 ğŸ¸ @author @xtremetom
 ğŸ¸ @dev An experiment in storing stupidly huge amounts of data on-chain and
 ğŸ¸      using it convert a 20k collection of animated gifs into an on-chain
 ğŸ¸      collection with future ability to make the whole collection dynamic
 ğŸ¸
 ğŸ¸      You can tell just how stupid this is by the number of ğŸ¸s
 ğŸ¸
 ğŸ¸
 ğŸ¸      ==================================
 ğŸ¸      ğŸ¸ TRAIT BUNDLE
 ğŸ¸      ==================================
 ğŸ¸      Each trait bundle has:
 ğŸ¸      - sprite sheet (png as bytes minus the first 24 bytes)
 ğŸ¸      - name (as bytes)
 ğŸ¸      - length of name (as bytes)
 ğŸ¸
 ğŸ¸      eg:
 ğŸ¸      4 | Blue | SpriteSheet.png
 ğŸ¸
 ğŸ¸      04 | 42 6c 75 65 | 06 c0 00 00 00 48 ...
 ğŸ¸
 ğŸ¸
 ğŸ¸      ==================================
 ğŸ¸      ğŸ¸ LAYER BUNDLE
 ğŸ¸      ==================================
 ğŸ¸      Each layer bundle is made of all the `trait bundle` related to that layer.
 ğŸ¸      When created, the index of the last byte for each `trait bundle`
 ğŸ¸      is recorded:
 ğŸ¸
 ğŸ¸      eg of trait bundles in a layer bundle:
 ğŸ¸      Alien | Ninja | Pirate
 ğŸ¸
 ğŸ¸      index of last trait bundle byte:
 ğŸ¸      157 | 350 | 600
 ğŸ¸
 ğŸ¸
 ğŸ¸      ==================================
 ğŸ¸      ğŸ¸ COLLECTION BUNDLE
 ğŸ¸      ==================================
 ğŸ¸      All the `layer bundles` are packed into a single collection bundle.
 ğŸ¸      When created, the last byte for each `layer bundle` is recorded:
 ğŸ¸
 ğŸ¸      eg of layer bundles in the collection bundle:
 ğŸ¸      backgrounds | bodies | hats | faces
 ğŸ¸
 ğŸ¸      index of last layer bundle byte:
 ğŸ¸      3500 | 6700 | 85_000 | 120_000 | 200_000
 ğŸ¸
 ğŸ¸
 ğŸ¸      ==================================
 ğŸ¸      ğŸ¸ STRUCTURE
 ğŸ¸      ==================================
 ğŸ¸
 ğŸ¸      Blue | Green | Orange        Alien | Ninja | Pirate        Smile | Frown | Laugh
 ğŸ¸      |                   |        |                    |        |                   |
 ğŸ¸      |                   |        |                    |        |                   |
 ğŸ¸      |                   |        |                    |        |                   |
 ğŸ¸      [LAYER BUNDLE --- BG]        [LAYER BUNDLE -- BODY]        [LAYER BUNDLE - FACE]
 ğŸ¸                |                             |                             |
 ğŸ¸                +--------------------+        |        +--------------------+
 ğŸ¸                                     |        |        |
 ğŸ¸                                     V        V        V
 ğŸ¸                                     [COLLECTION BUNDLE]
 ğŸ¸ ================================================================================*/

import "./lib/scripty2/interfaces/IContractScript.sol";
import "./lib/SmallSolady.sol";
import "./BundleManager.sol";
import "./lib/bytes/BytesLib.sol";

contract OnchainRenderer {

    using BytesLib for bytes;

    struct Layer {
        bytes name;
        bytes dom;
    }

    struct Settings {
        bool one_one;
        Layer backgroundLayer;
        Layer backLayer;
        Layer bodyLayer;
        Layer faceMaskLayer;
        Layer hatLayer;
        Layer faceLayer;
        Layer frontLayer;
    }

    address public immutable _scriptyStorageAddress;
    address public immutable _bundleManagerAddress;

    string constant IMG_HEADER = "iVBORw0KGgoAAAANSUhEUgAA";

    constructor(
        address scriptyStorageAddress,
        address bundleManagerAddress
    ) {
        _scriptyStorageAddress = scriptyStorageAddress;
        _bundleManagerAddress = bundleManagerAddress;
    }

    /**
     ğŸ¸ @notice Get to metadata for a given token ID
     ğŸ¸ @param tokenId - Token ID for desired token
     ğŸ¸ @return metadata as a string
     */
    function tokenURI(uint256 tokenId) public view returns (string memory) {

        string[2] memory jsonData = buildJSONData(tokenId);

        bytes memory json = abi.encodePacked(
            '{"name":"',
            'Baby Pepe: #',
            SmallSolady.toString(tokenId),
            '", "description":"',
            'Baby Pepes are mischievous little on-chain frogs that love memes and fun.',
            '","image":"data:image/svg+xml;base64,',
            jsonData[0],
            '",',
            jsonData[1],
            '}'
        );

        return string(
            abi.encodePacked(
                "data:application/json,",
                json
            )
        );
    }

    /**
     ğŸ¸ @notice Build the metadata json information
     ğŸ¸ @param tokenId - Token ID for desired token
     ğŸ¸ @return jsonData - array of strings for image and attributes
     */
    function buildJSONData(uint256 tokenId) internal view returns (string[2] memory jsonData) {

        Settings memory settings = buildSettings(tokenId);

        string memory svgHeader = '<svg class="a p" viewBox="0 0 72 72" xmlns="http://www.w3.org/2000/svg"><style>.a{width:576px;height:576px}.p{image-rendering:-webkit-optimize-contrast;-ms-interpolation-mode:nearest-neighbor;image-rendering:-moz-crisp-edges;image-rendering:pixelated}@keyframes f24{100%{background-position:-1728px 0}}div{width:72px;height:72px;animation:f24 2.4s steps(24) infinite}</style><svg class="p" viewBox="0 0 72 72" xmlns="http://www.w3.org/2000/svg">';
        string memory svgFooter = '</svg></svg>';

        // custom handling for Sexy
        if (settings.backgroundLayer.name.equal("Sexy")) {
            svgHeader = '<svg class="a p" viewBox="0 0 72 72" xmlns="http://www.w3.org/2000/svg"><style>.a{width:576px;height:576px}.p{image-rendering:-webkit-optimize-contrast;-ms-interpolation-mode:nearest-neighbor;image-rendering:-moz-crisp-edges;image-rendering:pixelated}@keyframes f67{100%{background-position:-4824px 0}}div{width:72px;height:72px;animation:f67 26.8s steps(67) infinite}</style><svg class="p" viewBox="0 0 72 72" xmlns="http://www.w3.org/2000/svg">';
        }

        jsonData[0] = SmallSolady.encode(
            abi.encodePacked(
                svgHeader,
                settings.backgroundLayer.dom,
                settings.backLayer.dom,
                settings.bodyLayer.dom,
                settings.faceMaskLayer.dom,
                settings.hatLayer.dom,
                settings.faceLayer.dom,
                settings.frontLayer.dom,
                svgFooter
            )
        );

        jsonData[1] = string(buildAttributes(settings));
    }

    /**
     ğŸ¸ @notice Build settings for tokenId
     ğŸ¸ @param tokenId - Token Id to build settings for
     ğŸ¸ @return settings - Settings for token Id
     ğŸ¸ @dev In the event that the token is a 1/1 it only requires a single layer
     ğŸ¸      as the 1/1 sprite sheet includes all layers in a single sheet.
     */
    function buildSettings(uint256 tokenId) internal view returns (Settings memory settings) {

        bytes[7] memory layerBundles = getLayerBundles();

        // Check for 1/1
        (bool is1_1, uint256 index) = isOneOfOne(tokenId);
        if (is1_1) {
            settings.backgroundLayer = buildLayer("one_one", layerBundles[6], index, "");
            settings.one_one = true;

            // set names for 1/1
            bytes memory name = settings.backgroundLayer.name;
            settings.bodyLayer.name = name;
            settings.hatLayer.name = name;
            settings.faceLayer.name = name;
            return settings;
        }

        BundleManager bundleManager = BundleManager(_bundleManagerAddress);

        // pull any custom traits for layers
        // - non custom traits return as an empty string ""
        bytes[4] memory customTraitBundleForLayers = bundleManager.getCustomTraitBundleForLayers(tokenId);

        bytes memory genePool = IContractScript(_scriptyStorageAddress).getScript("baby_pepes_genes", "");
        (uint8 backgroundIndex, uint8 bodyIndex, uint8 hatIndex, uint8 faceIndex) = getTraitIndices(tokenId, genePool);


        //==================================
        // ğŸ¸ MAIN LAYERS:
        //==================================

        // Background
        settings.backgroundLayer = buildLayer("background", layerBundles[0], backgroundIndex, customTraitBundleForLayers[0]);

        // Body
        settings.bodyLayer = buildLayer("body", layerBundles[2], bodyIndex, customTraitBundleForLayers[1]);

        // Hat
        settings.hatLayer = buildLayer("hat", layerBundles[3], hatIndex, customTraitBundleForLayers[2]);

        // Face
        // If face needs masking, that means we have show it behind the hat
        // This is needed for faces like "ninja"
        Layer memory faceLayer = buildLayer("face", layerBundles[4], faceIndex, customTraitBundleForLayers[3]);
        (faceRequiresMasking(faceIndex))
        ? settings.faceMaskLayer = faceLayer
        : settings.faceLayer = faceLayer;


        //==================================
        // ğŸ¸ SUB LAYERS:
        //==================================

        // Back (sublayer)
        if (settings.hatLayer.name.equal("Moon Warrior")) {
            settings.backLayer = buildLayer("back", layerBundles[1], 0, "");
        }

        // Front (sublayer)
        if (settings.bodyLayer.name.equal("Catchum")) {
            settings.frontLayer = buildLayer("front", layerBundles[5], 0, "");
        }
        else if (settings.bodyLayer.name.equal("Metal Head")) {
            settings.frontLayer = buildLayer("front", layerBundles[5], 1, "");
        }
    }

    /**
     ğŸ¸ @notice Build a single layer
     ğŸ¸ @param layerName - Layer being built
     ğŸ¸ @param layerBundle - Layer bundle to splice from
     ğŸ¸ @param index - Location of the required trait bundle
     ğŸ¸ @param customTraitBundle - Custom trait bundle | ""
     ğŸ¸ @return layer - Final layer ready for use
     ğŸ¸ @dev
     ğŸ¸      ==================================
     ğŸ¸      ğŸ¸ Custom Trait Bundle:
     ğŸ¸      ==================================
     ğŸ¸      This is favored over extracting trait bundle from layer bundle
     ğŸ¸
     ğŸ¸      [Custom Bundle Structure]
     ğŸ¸      unit8 name length | name | [sprite sheet as bytes]
     ğŸ¸
     ğŸ¸      [Example]
     ğŸ¸      03Bob{sprite sheet bytes}
     ğŸ¸
     ğŸ¸
     ğŸ¸      ==================================
     ğŸ¸      ğŸ¸ Layer Bundles:
     ğŸ¸      ==================================
     ğŸ¸      These are huge as explained in `getStartAndLength()`
     ğŸ¸      This function uses start and length splicing params to cut
     ğŸ¸      the required trait bundle from the layer bundle
     ğŸ¸
     ğŸ¸      [Structure]:
     ğŸ¸      03Cap{sprite sheet bytes}...06Beanie{sprite sheet bytes}...06Helmet{sprite sheet bytes}...
     ğŸ¸
     ğŸ¸      [Cut]:
     ğŸ¸      03Cap{sprite sheet bytes}...06Beanie{sprite sheet bytes}...06Helmet{sprite sheet bytes}...
     ğŸ¸                                  |                          |
     ğŸ¸                                  |                          |
     ğŸ¸                           [start][----------length----------]
     ğŸ¸                                  |                          |
     ğŸ¸                                  |                          |
     ğŸ¸                                  06Beanie{sprite sheet bytes}
     ğŸ¸
     ğŸ¸
     ğŸ¸      ==================================
     ğŸ¸      ğŸ¸ Building Sprite Sheets
     ğŸ¸      ==================================
     ğŸ¸      To save on deployment costs the sprite sheets are stored in the layer bundles as bytes.
     ğŸ¸      Not as bytes of the base64 encoded image. I also trim off the first 24 bytes as these are
     ğŸ¸      identical for all the sprite sheets.
     ğŸ¸
     ğŸ¸      To use the sprite sheet we first base64 encode the png bytes and then add the IMG_HEADER
     */
    function buildLayer (
        string memory layerName,
        bytes memory layerBundle,
        uint256 index,
        bytes memory customTraitBundle
    ) internal view returns (Layer memory layer) {

        bytes memory name;
        bytes memory spriteSheet;
        bytes memory traitBundle;

        // use custom or splice from layerBundle
        if (customTraitBundle.length > 0) {
            traitBundle = customTraitBundle;
        } else {
            (uint256 start, uint256 length) = getTraitBundleStartAndLength(index, layerName);
            traitBundle = layerBundle.slice(start, length);
        }

        (name, spriteSheet) = getNameAndSpriteSheet(traitBundle);
        layer.name = name;

        // Base64 encode raw sprite sheet png bytes
        string memory base64SpriteSheet = SmallSolady.encode(spriteSheet);

        if (spriteSheet.length > 0) {
            layer.dom = abi.encodePacked(
                '<foreignObject x="0" y="0" width="100%" height="100%">',
                '<div xmlns="http://www.w3.org/1999/xhtml" style="background-image:url(data:image/png;base64,', IMG_HEADER, base64SpriteSheet, ')"></div>',
                '</foreignObject>'
            );
        }
    }

    /**
     ğŸ¸ @notice Build single attribute
     ğŸ¸ @param key - Attribute key
     ğŸ¸ @param value - Attribute value
     ğŸ¸ @return trait - Attribute as bytes
     */
    function buildAttributeTrait(bytes memory key, bytes memory value) internal pure returns (bytes memory trait) {
        return abi.encodePacked('{"trait_type":"', key, '","value": "', value, '"}');
    }

    /**
     ğŸ¸ @notice Build metadata attributes
     ğŸ¸ @param settings - Settings for this token
     ğŸ¸ @return attr - Attributes as bytes
     */
    function buildAttributes(Settings memory settings) internal pure returns (bytes memory attr) {

        bytes memory babyType = "Baby";
        if (settings.backgroundLayer.name.equal("Gallery")) babyType = "Gallery";
        else if (settings.one_one) babyType = "1/1";

        // other tokens
        return abi.encodePacked(
            '"attributes": [',
                buildAttributeTrait("Background", settings.backgroundLayer.name),
                ',',
                buildAttributeTrait("Body", settings.bodyLayer.name),
                ',',
                buildAttributeTrait("Hat", settings.hatLayer.name),
                ',',
                buildAttributeTrait("Face", settings.faceLayer.name),
                ',',
                buildAttributeTrait("Type", babyType),
            ']'
        );
    }

    /**
     ğŸ¸ @notice Check if the token Id is a 1/1
     ğŸ¸ @param tokenId - Token ID for desired token
     ğŸ¸ @return isOneOfOne - bool 1/1
     ğŸ¸ @return i - index of 1/1
     */
    function isOneOfOne(uint256 tokenId) internal view returns (bool isOneOfOne, uint256 i) {
        uint16[7] memory list = BundleManager(_bundleManagerAddress).getOneOfOneList();

        unchecked {
            do {
                if (uint256(list[i]) == tokenId) isOneOfOne = true;
            } while (!isOneOfOne && ++i < 7);
        }

        return (isOneOfOne, i);
    }

    /**
     ğŸ¸ @notice Determine if a face trait needs to be masked
     ğŸ¸ @param traitId - Id of the trait to check
     ğŸ¸ @return requiresMasking - Boolean of masking needs
     ğŸ¸ @dev Masking is needed if the face trait crosses over the hat traits
     ğŸ¸      To fix this, the face trait is moved behind the hat trait
     */
    function faceRequiresMasking(uint256 traitId) internal view returns (bool requiresMasking) {
        uint8[] memory list = BundleManager(_bundleManagerAddress).getFaceMaskingList();
        uint256 len = list.length;
        uint256 i;

        unchecked {
            do {
                if (uint256(list[i]) == traitId) requiresMasking = true;
            } while (!requiresMasking && ++i < len);
        }
    }

    // =============================================================
    //                           UTILS
    // =============================================================

    /**
     ğŸ¸ @notice Split the collection bundle into layer bundles
     ğŸ¸ @return layerBundles - Array of layer bundles
     ğŸ¸ @dev See explanation starting at line 32
     */
    function getLayerBundles() internal view returns (bytes[7] memory layerBundles) {
        bytes memory collectionBundle = IContractScript(_scriptyStorageAddress).getScript("baby_pepes_all_layers", "");
        uint24[7] memory layerByteBoundaries = BundleManager(_bundleManagerAddress).getLayerByteBoundaries();

        uint256 i;
        uint256 start;
        uint256 length;
        unchecked {
            do {
                if (i > 0) start = uint256(layerByteBoundaries[i - 1]);
                length = uint256(layerByteBoundaries[i]) - start;

                layerBundles[i] = collectionBundle.slice(start, length);
            } while (++i < 7);
        }
    }

    /**
     ğŸ¸ @notice Split gene data into trait ids
     ğŸ¸ @param tokenId - Id of token to get data for
     ğŸ¸ @return backgroundTraitId - Trait id for background layer
     ğŸ¸ @return bodyTraitId - Trait id for body layer
     ğŸ¸ @return hatTraitId - Trait id for hat layer
     ğŸ¸ @return faceTraitId - Trait id for face layer
     ğŸ¸ @dev Genes is a huge data set containing the trait Ids for
     ğŸ¸      the whole collection
     ğŸ¸
     ğŸ¸      [Structure]:
     ğŸ¸      0101010105050535258901
     ğŸ¸
     ğŸ¸      [Example]:
     ğŸ¸      01 01 01 01    |   05 05 05 05      |   35 25 89 01
     ğŸ¸      Baby Pepe #1   |   Baby Pepe #2     |   Baby Pepe #3
     */
    function getTraitIndices(uint256 tokenId, bytes memory genePool) internal pure returns (
        uint8 backgroundTraitId,
        uint8 bodyTraitId,
        uint8 hatTraitId,
        uint8 faceTraitId
    ) {
        assembly {
            let offset := mul(sub(tokenId, 1), 4)
            let genes := mload(add(genePool, add(offset, 4)))
            backgroundTraitId := shr(24, genes)
            bodyTraitId := shr(16, genes)
            hatTraitId := shr(8, genes)
            faceTraitId := shr(0, genes)
        }
    }

    /**
     ğŸ¸ @notice Get the start and length of splice for a trait by index
     ğŸ¸ @param index - Index of the required bundle
     ğŸ¸ @param layerName - Name of the required layer bundle
     ğŸ¸ @return start - slicing start point
     ğŸ¸ @return length - slicing length
     ğŸ¸ @dev Using the known byte boundaries for the traits within the layer bundle, we can get the
     ğŸ¸      the starting bytes and total byte length for a desired trait bundle the layer bundle
     ğŸ¸
     ğŸ¸      traitBundle[0] starts at 1
     ğŸ¸      traitBundle[N] starts from traitBundle[N-1]+1
     ğŸ¸
     ğŸ¸      [Layer Bundle Structure]:
     ğŸ¸      03Cap{sprite sheet bytes}...06Beanie{sprite sheet bytes}...06Helmet{sprite sheet bytes}...
     ğŸ¸
     ğŸ¸      [Byte Boundaries Structure]:
     ğŸ¸      [150, 350, 600]
     ğŸ¸
     ğŸ¸      [Example Trait Slicing]:
     ğŸ¸      [1, 150]        |   [151, 350]      |   [351, 600]
     ğŸ¸      Trait #0        |   Trait #1        |   Trait #2
     */
    function getTraitBundleStartAndLength(uint256 index, string memory layerName) internal view returns (uint256 start, uint256 length) {

        uint256 end;

        string memory slotsFunction = string.concat("_", layerName, "Slots(uint256)");

        unchecked {
            if (index > 0) {
                (, bytes memory startData) = _bundleManagerAddress.staticcall(abi.encodeWithSignature(slotsFunction, index - 1));
                start = abi.decode(startData, (uint256));
            }

            (, bytes memory endData) = _bundleManagerAddress.staticcall(abi.encodeWithSignature(slotsFunction, index));
            end = abi.decode(endData, (uint256));

            return (start, end-start);
        }
    }

    /**
     ğŸ¸ @notice Get the sprite sheet and name from trait bundle
     ğŸ¸ @param bundle - bundle to split up
     ğŸ¸ @return name - Name of the trait
     ğŸ¸ @return spriteSheet - Sprite sheet of the trait
     */
    function getNameAndSpriteSheet(bytes memory bundle) internal view returns(bytes memory name, bytes memory spriteSheet) {
        uint8 nameLength;
        assembly {
            nameLength := mload(add(bundle, 1))
        }

        unchecked {
            uint256 offset = 1 + nameLength;
            name = bundle.slice(1, nameLength);
            spriteSheet = bundle.slice(offset, bundle.length - offset);
        }
    }
}