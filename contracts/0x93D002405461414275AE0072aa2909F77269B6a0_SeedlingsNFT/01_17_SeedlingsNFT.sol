// SPDX-License-Identifier: MIT

//
//                                                                                           .             ..      . .
//                                                                           _.      .        :           =ns    ...:-..
//                                                                .         .|+:  ... .-..|_ .:;         =nft;   ._sUNc                          ._
//                                                               .J,.       :++:..       =||; :;...      |vl}:_  .ALOEx_,               ....:....%ii
//                          ...                            =;    ."'.       :::.  .     .+|++.:;. . :.  .+|=;--:+=IoS2+-+<__...   .__;=:---:::;;=+:|
//             .        ...:-.:.:.:.. .. . ........       .|+;    ..     ....::-:. .     ||==:.:.    .:::::=~   .;=|I( ...=<|====++- .   . =<i=;:::=|
//         ...:....    ..._<a%,::.._;. . .:::-:::..... ...====..-... .. .    .-=%.       :=;:sun:     .:Mwl:.   :s;=-. :.:<nft^,::: .    .=PLANT/..::
//        ..:==>;-.......-dSEEDmoonX;    :.;=%====;;::. .  :;;:;:::.. .     ..]SD..        ..xS!:.   . =OSu;:.:::>::. .-=vSEED>#;:::    .=i#GROWX;.:.
//       ..UNIQUE;:.    .:3Z!?!!!!!!` . -:=ZdHARVESTa;    .:seeds!>;...     .:]EE:.         :)Er:.    .=OOnc:. -=Mz:. ..<GROWINGv> ; .  .=EARN*|Y';..
//      ..-vSEEDSe:    ..:3G:-.. .. ... ..<#@TIME!#ZX;.   .=Z#SEEDSo%:      ..]EF;.        .:pEe; .  . =NLa+:::.=Oe:.  :EARN(===S2.::   :=HA(+:::<; .
//      ..:n+:+!1+..    .-3R:.    . ;:.  :)PX+++||*!!`.=...=ZZGROWx#X=:.    ..]DI=..       .:lDG:;+=: .:eArn;=..=Oe: ..:)UP+:-:::--:=,  ==RV;;.. )( -
//     ...)f:.:..:...  ...3o:..         .:)Oe:::;;..  .== .=XzEARNYxNft.. .  :)SX=:        ..aXR;=||` .:3RZ#C;..<Ne:.. :=3(:. .=+:.:|=. .:ES:.:  . ..
//      ..)t:....)=..  ...3W:. .    .   ..]LC:-:;;=.   - .:;<%:.-.:+FUSc.   ..)GE>..        :nRO:;+: ..<GROWX;=.=Xe;=;.<U;;..  -- -=:-  . |Ta...   .
//     ...=X>.  .-`.   :..)~::.-..    .=:-]Yc:.....      -.=MR;...-:TtEo:.   .<RA>.        ..fEW:.-   .=EARNZc;:<Na;+=;3N(::.. . ..      ..;S>--...
//    :=.::G|:.  ...   ...=s<ass=:.  :==:-)G+:.:--       .:=UO;.  :.<iEH>-. ..=OR>.._       :tDs-    ..<SEEDSN;:=Fr;===dI>-::.. .: .. .|=_..{Es.:..
//   :=;;.:)Rc...       .:dPLANTX;    :=::=O=>===:.     ...=SO;. . .=lDac.  ..=WN=;=+`     .:dIH:.   ::<E2=)#Fc;<Tl:--:3Q>:. .grow;,.:.iii=.:3Ec..:.
//   -:;: -:3O>:...;   .-:REWARDS:.    -::3NFTSXrc     .++==HM;..  .:iLr[..:=+=defi++.     .:eSA::. .::<EX==3TP=iXy;..:3U(: .Sprout} ..||||:.-3Ec..;=
//        .:+#Wp:===.  ..:ha+===;.  .  -::utility(     .+=;:Xe:.   ..tIv(..;===SPc;+= .    .:fTR;.  .:.<D2;:+#LShDc:. :]Ec:...PETALs . |||=`..=3D;::::
//         .:{#o;==+.   .:ar:.:==:;|+.  :=X#*???Y'.     =+;;=<;     :yNe;..==;=eRe.--      .:iRV;.   ..<Lk:.:3AEaEc:...)eX::..:leaf:.:;:=::-...+XS..:-
//    .. ...::Xl;==+   ..:rn:.. :=+++. =:=Z#=::-...      :::dZ=. . .:=Gs>. --:;50%:.       .:eIE=. .  .<Ig;.:<NErF[::.:=XX>:::;;<ce-::=:.:>....:)X;-.
//    . .d. .:)y>:--    .:vD;....+|+|. =:=MW=:.        __.:=ZZ=..-..=3St(    .start...   . .:aBS;.    .)Nr;..:TDvI[:.. :)}=>;==idue.:==;.:X;::-:=Z(..
//   ...=e...::g[..   ._..ee;....===:  ::<AEE.. .      |++==#Z>-.. :<#xZ: .  .utility-... ..:rUT:.    .)Go;::;<SeEc:.  .-suSUNw#Zt(::=+:.:3h::=|uU=..
//    . =f..:::o(..  .==..sf;;======: ..:<TT=:.. .    .=+=;=Z#=-..:=grOw .   .:Nft==;:::;;;.;nTX:.    .<Sw:::;;3sA[.  ...=FLOWERZe; .-:. :)e=MOONo::
//    ..-i:<earn`.   .==..ti=uSEEDSX;.  :=IH=::.:....  :|;;=#Z>:magical:-.   .:nxPLANT%eXZc..]EZ:== . .:=(..=:.:tR;: ..::.=SEEDS(;..     ..=SEEDS(.::
//     ..HARVEST:..  .=:. ._dLIFTOFF=. ..+C(<plant>;:   --:=#10,000X#X(..    ..gIGROWXuXXZc..i>l:;+:   =Y(.   ..{N(. .:;: .-+*|:: .       .-STAR(::=:
//      .xSEEDSa..      ..:TOGETHERa:. ...:seedlings:     :=XMAGICALX(..      .zsEARNX?!!!^.."!"::+`   .        -~` .:=: . .. ...          ..+"+:--:
//      ..-!!+:..       .::--------- .  :.SUNFLOWER2:=.  ..=XSEEDSX2+-.       .:=+=----.... .                      .   .     .              .....
//        . ...         -== . ..... .   . -=++!!!!?*=|+   .=?Y(|**+:-:;..      .++=... .
//                                       .. .......-+|+ .  ..........=|;       .+=+.
//                                          . .  .  -=|.       . .   =++        -==
//                                                    .                -
//

pragma solidity ^0.8.7;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/common/ERC2981.sol";
import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";


contract SeedlingsNFT is ERC721, ERC721Enumerable, ERC2981, Ownable, ReentrancyGuard {

    using SafeMath for uint256;

    uint256 public mintPrice; 

    uint256 public devReserve;

    uint256 public maxSupply; 

    uint256 public maxPerWallet;

    uint256 public maxPerPurchase;

    uint256 public devMinted;

    uint256 public mintDate;

    bool public isPublicMintEnabled;

    string internal baseTokenUri;

    mapping(address => uint256) public walletMints;

    constructor() payable ERC721('Seedlings', 'SEED') {

        mintPrice = 0.08 ether;
        maxSupply = 10000;
        maxPerWallet = 25;
        maxPerPurchase = 5;
        devReserve = 25;
        devMinted = 0;
    }

    function setIsPublicMintEnabled(bool isPublicMintEnabled_) external onlyOwner {

        isPublicMintEnabled = isPublicMintEnabled_;
    }

    function setMintDate(uint256 _mintDate) external onlyOwner {
	
        mintDate = _mintDate;
    }

    function _beforeTokenTransfer(address from, address to, uint256 tokenId) internal override(ERC721, ERC721Enumerable) {

        super._beforeTokenTransfer(from, to, tokenId);
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC721, ERC721Enumerable, ERC2981) returns (bool) {

        return super.supportsInterface(interfaceId);
    }

    function setMaxPerWallet(uint256 _maxPerWallet) external onlyOwner {

        maxPerWallet = _maxPerWallet;
    }

    function setMaxPerPurchase(uint256 _maxPerPurchase) external onlyOwner {

        maxPerPurchase = _maxPerPurchase;
    }

    //=====================+
    //         URI         |
    //=====================+
    function setBaseTokenUri(string calldata baseTokenUri_) external onlyOwner {

        baseTokenUri = baseTokenUri_;
    }

    function tokenURI(uint256 tokenId_) public view override returns (string memory) {

        require(_exists(tokenId_), 'Token does not exist');

        return string(abi.encodePacked(baseTokenUri, Strings.toString(tokenId_), ".json"));
    }

    function setDefaultRoyalty(address receiver, uint96 feeNumerator) external onlyOwner {

        _setDefaultRoyalty(receiver, feeNumerator);
    }

    //=====================+
    //       Withdraw      |
    //=====================+
    function withdraw() external payable onlyOwner nonReentrant {

        payable(msg.sender).transfer(address(this).balance);
    }

    //=====================+
    //       Dev Mint      |
    //=====================+
    function devMint(address _receiver, uint256 n) external payable nonReentrant onlyOwner {

        uint256 supply = totalSupply();
        uint256 devSupply = devMinted.add(n);
        require(devSupply <= devReserve, 'dev mint limit reached');
        require(devSupply.add(supply) <= maxSupply, 'max supply reached');

        devMinted = devSupply;

        for (uint256 i = 0; i < n; ++i) {

            _safeMint(_receiver, supply + i);
        }
    }

    //=====================+
    //        Mint         |
    //=====================+
    function mint(uint256 quantity_) external payable nonReentrant {

        require(isPublicMintEnabled, 'Minting not enabled');

        require(quantity_ <= maxPerPurchase, "Maximum per purchase exceeded.");  

        uint256 ts = totalSupply();

        require(ts.add(quantity_) <= maxSupply, 'Sold out');

        uint256 totalCost = quantity_.mul(mintPrice);

        require(msg.value >= totalCost, 'Insufficient funds');

        uint256 refundAmount = msg.value.sub(totalCost);

        require(walletMints[msg.sender] + quantity_ <= maxPerWallet, 'Maximum per wallet exceeded.');

        walletMints[msg.sender] += quantity_;

        for (uint256 i = 0; i < quantity_; ++i) {

            _safeMint(msg.sender, ts + i);
        }

        if (refundAmount > 0) {
            payable(msg.sender).transfer(refundAmount);
        }
    }

}