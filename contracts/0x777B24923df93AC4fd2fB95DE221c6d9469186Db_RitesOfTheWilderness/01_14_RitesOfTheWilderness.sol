// SPDX-License-Identifier: GPL-3.0
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  @@@@@@  @@@@            @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    @@@@    @@@@              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@              @@                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@                                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@            @@@@                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                  @@@@    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@          @@@@@@    @@      @@@@          @@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    @@    @@@@@@    @@@@    @@@@@@@@    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  @@@@  @@@@@@@@  @@@@@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    @@    @@@@@@    @@@@    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//
// The Barren Court
// https://twitter.com/thebarrencourt
pragma solidity 0.8.9;

import "Ownable.sol";
import "ReentrancyGuard.sol";
import "ERC1155.sol";
import "ERC1155Holder.sol";
import "IBurnableERC1155.sol";

/**
 * @dev The Rites of the Wilderness
 */
contract RitesOfTheWilderness is Ownable, ReentrancyGuard, ERC1155Holder {
    /// @notice The bestiary token id of the Pile of Sand and Guts
    uint256 public constant PILE_ID = 13;

    /// @notice The number of burial sites
    uint256 public constant NUM_BURIAL_SITES = 8;

    /// @notice The address of the Bestiary ERC-1155 contract
    address public bestiaryAddress;

    /// @notice The address of the Barren Court guild wallet
    address public courtAddress;

    string public constant R =
        "Scattered across the ancient desert are eight burial sites, where the remains of long-forgotten, primordial beasts lie at rest. When the last sun sets, and the Wyrmhole finally opens, those who come across these hallowed grounds will face a choice that they must live with for the rest of their days, and a steep toll must be exacted. Release a number of unique familiars back into the wilds and from the ashes in the sand will rise a child of the desert reborn, returned to this barren land by the rites of the wilderness.";

    event Summon(uint256 indexed bestiaryTokenId, address summonedBy);

    event Claim(
        uint256 indexed bestiaryTokenId,
        uint256 indexed claimNumber,
        address claimedBy
    );

    mapping(uint256 => bool) public hasBeenSummoned; // maps bestiary tokenId to whether it has been summoned or not.
    mapping(uint256 => uint256) public claimCount; // maps bestiary tokenId to number of times it has been claimed

    constructor(address _bestiaryAddress, address _courtAddress) {
        setBestiaryAddress(_bestiaryAddress);
        setCourtAddress(_courtAddress);
    }

    /**
     * @dev Set the address of the Bestiary ERC-1155 contract
     * @param newBestiaryAddress address the address to the contract
     */
    function setBestiaryAddress(address newBestiaryAddress) public onlyOwner {
        bestiaryAddress = newBestiaryAddress;
    }

    /**
     * @dev Set the address of the Barren Court guild wallet
     * @param newCourtAddress address the address to the wallet
     */
    function setCourtAddress(address newCourtAddress) public onlyOwner {
        courtAddress = newCourtAddress;
        transferOwnership(courtAddress);
    }

    function safeBatchOwnerClaim(
        uint256[] calldata ids,
        uint256[] calldata amounts
    ) public onlyOwner {
        ERC1155(bestiaryAddress).safeBatchTransferFrom(
            address(this),
            msg.sender,
            ids,
            amounts,
            ""
        );
    }

    function summon(
        uint256 bestiaryTokenId,
        uint256[] calldata releasedIds,
        uint256[] calldata releasedAmounts
    ) public {
        require(checkBestiaryTokenId(bestiaryTokenId), "Invalid burial site");
        require(
            !hasBeenSummoned[bestiaryTokenId],
            "That creature has already been summoned..."
        );
        uint256[] memory tokenIds = new uint256[](1);
        tokenIds[0] = PILE_ID;
        uint256[] memory amounts = new uint256[](1);
        amounts[0] = 1;
        IBurnableERC1155(bestiaryAddress).burn(msg.sender, tokenIds, amounts);
        hasBeenSummoned[bestiaryTokenId] = true;
        emit Summon(bestiaryTokenId, msg.sender);
        claim(bestiaryTokenId, releasedIds, releasedAmounts);
    }

    function claim(
        uint256 bestiaryTokenId,
        uint256[] calldata releasedIds,
        uint256[] calldata releasedAmounts
    ) public {
        require(checkBestiaryTokenId(bestiaryTokenId), "Invalid burial site");
        require(
            hasBeenSummoned[bestiaryTokenId],
            "That creature has not yet been summoned..."
        );
        require(
            ERC1155(bestiaryAddress).balanceOf(address(this), bestiaryTokenId) >
                0,
            "That burial site has closed"
        );
        uint256 cost = releaseRequirement(bestiaryTokenId);
        require(
            releasedIds.length == cost,
            "You did not meet the requirements... (releasedIds.length != cost)"
        );
        claimCount[bestiaryTokenId] += 1;
        ERC1155(bestiaryAddress).safeBatchTransferFrom(
            msg.sender,
            address(this),
            releasedIds,
            releasedAmounts,
            ""
        );
        for (uint256 i = 0; i < cost; i++) {
            require(
                ERC1155(bestiaryAddress).balanceOf(
                    address(this),
                    releasedIds[i]
                ) == 1,
                "You did not meet the requirements... (releasedAmounts[x] != 1)"
            );
        }
        ERC1155(bestiaryAddress).safeBatchTransferFrom(
            address(this),
            courtAddress,
            releasedIds,
            releasedAmounts,
            ""
        );
        ERC1155(bestiaryAddress).safeTransferFrom(
            address(this),
            msg.sender,
            bestiaryTokenId,
            1,
            ""
        );
        emit Claim(
            bestiaryTokenId,
            claimCount[bestiaryTokenId] - 1,
            msg.sender
        );
    }

    function releaseRequirement(uint256 bestiaryTokenId)
        public
        view
        returns (uint256)
    {
        require(checkBestiaryTokenId(bestiaryTokenId), "Invalid burial site");
        return bestiaryTokenId - PILE_ID + claimCount[bestiaryTokenId];
    }

    function burialSiteOpen(uint256 bestiaryTokenId)
        public
        view
        returns (bool)
    {
        require(checkBestiaryTokenId(bestiaryTokenId), "Invalid burial site");
        return (ERC1155(bestiaryAddress).balanceOf(
                address(this),
                bestiaryTokenId
            ) > 0);
    }

    function checkBestiaryTokenId(uint256 bestiaryTokenId)
        public
        pure
        returns (bool)
    {
        return
            (bestiaryTokenId > PILE_ID) &&
            (bestiaryTokenId <= PILE_ID + NUM_BURIAL_SITES);
    }
}