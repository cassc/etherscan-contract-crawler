//SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import 'erc721a-upgradeable/contracts/ERC721AUpgradeable.sol';
import '@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol';
import {DefaultOperatorFiltererUpgradeable} from "../../shared/opensea/upgradeable/DefaultOperatorFiltererUpgradeable.sol";
import "@openzeppelin/contracts/token/common/ERC2981.sol";

contract WagdieBeasts is ERC721AUpgradeable, OwnableUpgradeable, DefaultOperatorFiltererUpgradeable, ERC2981 {

    uint256 public toll;
    uint256 public beastPrice;
    bool public isMintingEnabled;
    string private baseURI;

    uint16 public constant maxBeasts = 2222;

    error MintingNotStarted();
    error ExceedsMaxMintQuantity();
    error ExceedsMaxSupply();
    error EthValueTooLow();
    error TollToHigh();

    event MintingEnabledChanged(bool isMintingEnabled);

    function initialize() initializerERC721A initializer public {
        __ERC721A_init('WAGDIE: Beasts', 'BEAST');
        __Ownable_init();
        __DefaultOperatorFilterer_init();

        toll = 570;
        beastPrice = 666 ether;
    }

    function _startTokenId() internal view virtual override returns (uint256) {
      return 1;
    }

    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override(ERC721AUpgradeable, ERC2981) returns (bool) {
        return 
            ERC721AUpgradeable.supportsInterface(interfaceId) || 
            ERC2981.supportsInterface(interfaceId);
    }

    /**
     *  @dev ð”—ð”žð”ªð”¢ ð”ž ð”Ÿð”¢ð”žð”°ð”± ð”£ð”¯ð”¬ð”ª ð”±ð”¥ð”¢ ð”‰ð”¬ð”¯ð”°ð”žð”¨ð”¢ð”« ð”ð”žð”«ð”¡ð”°.
     */
    function tameBeast(uint256 quantity) external payable {
        if (!isMintingEnabled) revert MintingNotStarted();
        if (quantity > 2) revert ExceedsMaxMintQuantity();
        if (quantity + totalSupply() > maxBeasts) revert ExceedsMaxSupply();
        if (msg.value < beastPrice * quantity) revert EthValueTooLow();
        require(msg.sender == tx.origin);

        _mint(msg.sender, quantity);
    }

    /**
     *  @dev ð”…ð”¢ð”°ð”±ð”¬ð”´ ð”±ð”¬ð”¨ð”¢ð”«ð”° ð”²ð”­ð”¬ð”« ð”±ð”¥ð”¬ð”°ð”¢ ð”¡ð”¢ð”¢ð”ªð”¢ð”¡ ð”´ð”¬ð”¯ð”±ð”¥ð”¶.
     */
    function bestowBeasts(address recipient, uint256 quantity) external onlyOwner {
        if (quantity + totalSupply() > maxBeasts) revert ExceedsMaxSupply();
        _mint(recipient, quantity);
    }

    /**
     *  @dev ð”–ð”žð” ð”¯ð”¦ð”£ð”¦ð” ð”¢ ð”ž ð”Ÿð”¢ð”žð”°ð”± ð”¶ð”¬ð”² ð”¬ð”´ð”«.
     */
    function burnBeast(uint256 tokenId) public virtual {
        _burn(tokenId, true);
    }

    /**
     *  @dev ð”–ð”¢ð”± ð”±ð”¥ð”¢ ð”­ð”¯ð”¦ð” ð”¢ ð”¬ð”£ ð”¢ð”žð” ð”¥ ð”Ÿð”¢ð”žð”°ð”±.
     */
    function setPrice(uint256 price) external onlyOwner {
        beastPrice = price;
    }

    /**
     *  @dev ð”ˆð”«ð”žð”Ÿð”©ð”¢ ð”±ð”¥ð”¢ ð”±ð”žð”ªð”¦ð”«ð”¤ ð”¬ð”£ ð”Ÿð”¢ð”žð”°ð”±ð”°.
     */
    function updateIsMintingEnabled(bool _isMintingEnabled) external onlyOwner {
        isMintingEnabled = _isMintingEnabled;
    }

    function setBaseURI(string memory baseURI_) external onlyOwner {
        baseURI = baseURI_;
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return baseURI;
    }

    /**
     *  @dev ð”–ð”¢ð”±ð”° ð”±ð”¬ð”©ð”© ð”£ð”¬ð”¯ ð”Ÿð”žð”¯ð”±ð”¢ð”¯ð”¦ð”«ð”¤ ð”¬ð”£ ð”±ð”¬ð”¨ð”¢ð”«ð”°.
     */
    function setToll(
        uint256 _toll
    ) external onlyOwner {
        if (_toll > 2500) revert TollToHigh();
        toll = _toll;
    }

    /**
     * @inheritdoc IERC2981
     */
    function royaltyInfo(
        uint256,
        uint256 _salePrice
    ) public view virtual override returns (address, uint256) {
        uint256 royaltyAmount = (_salePrice * toll) / 10000;
        return (owner(), royaltyAmount);
    }

    // OpenSea Operator Filter Overrides
    function setApprovalForAll(address operator, bool approved) public override onlyAllowedOperatorApproval(operator) {
        super.setApprovalForAll(operator, approved);
    }

    function approve(address operator, uint256 tokenId) public override onlyAllowedOperatorApproval(operator) {
        super.approve(operator, tokenId);
    }

    function transferFrom(address from, address to, uint256 tokenId) public override onlyAllowedOperator(from) {
        super.transferFrom(from, to, tokenId);
    }

    function safeTransferFrom(address from, address to, uint256 tokenId) public override onlyAllowedOperator(from) {
        super.safeTransferFrom(from, to, tokenId);
    }

    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory data)
        public
        override
        onlyAllowedOperator(from)
    {
        super.safeTransferFrom(from, to, tokenId, data);
    }
}