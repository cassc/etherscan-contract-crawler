// SPDX-License-Identifier: MIT
// Forked from operator-filter-registry v1.4.2 (operator-filter-registry/src/UpdatableOperatorFilterer.sol)

pragma solidity ^0.8.20;

import {IOperatorFilterRegistry} from "../../interfaces/IOperatorFilterRegistry.sol";
import {UpdatableOperatorFilterStorage} from "../../libraries/storage/UpdatableOperatorFilterStorage.sol";

/**
 *  ╔╗  ╔╗╔╗      ╔╗ ╔╗     ╔╗
 *  ║╚╗╔╝╠╝╚╗     ║║ ║║     ║║
 *  ╚╗║║╔╬╗╔╬═╦╦══╣║ ║║  ╔══╣╚═╦══╗
 *   ║╚╝╠╣║║║╔╬╣╔╗║║ ║║ ╔╣╔╗║╔╗║══╣
 *   ╚╗╔╣║║╚╣║║║╚╝║╚╗║╚═╝║╔╗║╚╝╠══║
 *    ╚╝╚╝╚═╩╝╚╩══╩═╝╚═══╩╝╚╩══╩══╝
 */

/**
 * @title  UpdatableOperatorFiltererInternal
 * @notice Abstract contract whose constructor automatically registers and optionally subscribes to or copies another
 *         registrant's entries in the OperatorFilterRegistry. This contract allows the Owner to update the
 *         OperatorFilterRegistry address via updateOperatorFilterRegistryAddress, including to the zero address,
 *         which will bypass registry checks.
 *         Note that OpenSea will still disable creator earnings enforcement if filtered operators begin fulfilling orders
 *         on-chain, eg, if the registry is revoked or bypassed.
 * @dev    This smart contract is meant to be inherited by token contracts so they can use the following:
 *         - `onlyAllowedOperator` modifier for `transferFrom` and `safeTransferFrom` methods.
 *         - `onlyAllowedOperatorApproval` modifier for `approve` and `setApprovalForAll` methods.
 *
 *         Updated to support EIP-2535 Diamond standard. Specifically, to support Diamond storage pattern. Also,
 *         most functions were made internal for reuse throughout other facets if necessary.
 */
abstract contract UpdatableOperatorFiltererInternal {
    using UpdatableOperatorFilterStorage for UpdatableOperatorFilterStorage.Layout;

    /// @dev Emitted when an operator is not allowed.
    error OperatorNotAllowed(address operator);
    /// @dev Emitted when someone other than the owner is trying to call an only owner function.
    error OnlyOwner();

    event OperatorFilterRegistryAddressUpdated(address newRegistry);

    /// @dev The function that acts as a constructor and is called through a DiamondInit
    function _initOperatorFilter(address _registry, address subscriptionOrRegistrantToCopy, bool subscribe) internal {
        IOperatorFilterRegistry registry = IOperatorFilterRegistry(_registry);
        UpdatableOperatorFilterStorage.Layout storage f = UpdatableOperatorFilterStorage.layout();
        f._setOperatorFilterRegistry(registry);
        // If an inheriting token contract is deployed to a network without the registry deployed, the modifier
        // will not revert, but the contract will need to be registered with the registry once it is deployed in
        // order for the modifier to filter addresses.
        if (address(registry).code.length > 0) {
            if (subscribe) {
                registry.registerAndSubscribe(address(this), subscriptionOrRegistrantToCopy);
            } else {
                if (subscriptionOrRegistrantToCopy != address(0)) {
                    registry.registerAndCopyEntries(address(this), subscriptionOrRegistrantToCopy);
                } else {
                    registry.register(address(this));
                }
            }
        }
    }

    /**
     * @dev A helper function to check if the operator is allowed.
     */
    modifier onlyAllowedOperator(address from) virtual {
        // Allow spending tokens from addresses with balance
        // Note that this still allows listings and marketplaces with escrow to transfer tokens if transferred
        // from an EOA.
        if (from != msg.sender) {
            _checkFilterOperator(msg.sender);
        }
        _;
    }

    /**
     * @dev A helper function to check if the operator approval is allowed.
     */
    modifier onlyAllowedOperatorApproval(address operator) virtual {
        _checkFilterOperator(operator);
        _;
    }

    /**
     * @notice Update the address that the contract will make OperatorFilter checks against. When set to the zero
     *         address, checks will be bypassed. OnlyOwner.
     */
    function _updateOperatorFilterRegistryAddress(address newRegistry) internal virtual {
        if (msg.sender != _owner()) {
            revert OnlyOwner();
        }
        UpdatableOperatorFilterStorage.Layout storage f = UpdatableOperatorFilterStorage.layout();
        f._setOperatorFilterRegistry(IOperatorFilterRegistry(newRegistry));
        emit OperatorFilterRegistryAddressUpdated(newRegistry);
    }

    /**
     * @dev Assume the contract has an owner, but leave specific Ownable implementation up to inheriting contract.
     */
    function _owner() internal view virtual returns (address);

    /**
     * @dev A helper function to check if the operator is allowed.
     */
    function _checkFilterOperator(address operator) internal view virtual {
        IOperatorFilterRegistry registry = UpdatableOperatorFilterStorage.layout()._operatorFilterRegistry();
        // Check registry code length to facilitate testing in environments without a deployed registry.
        if (address(registry) != address(0) && address(registry).code.length > 0) {
            // under normal circumstances, this function will revert rather than return false, but inheriting contracts
            // may specify their own OperatorFilterRegistry implementations, which may behave differently
            if (!registry.isOperatorAllowed(address(this), operator)) {
                revert OperatorNotAllowed(operator);
            }
        }
    }

    /**
     * @dev Returns the address of the current operatorFilterRegistry
     */
    function _operatorFilterRegistry() internal view returns (address) {
        return address(UpdatableOperatorFilterStorage.layout()._operatorFilterRegistry());
    }
}