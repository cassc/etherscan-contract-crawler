//
// ,KMMWkxK0kkO0NMKl.                                            .lNMXkxOKKKKKKkdKWx.
//  :XMM0xOKKKOkkOXWO,                                          .xWMKxx0KKKKKKOd0Xo.
//   cXMWkx0KKKK0kxOXXo.                                       .kWWKxkKKKKKKKOd0K:
//    cXMXxx0KKK0K0kx0Nx;:'                                   .OWW0dOK0KKKK0kx0k,
//     :KMNkxOKKKKKK0xkKX0c                                  .xWW0dOKKKKOOOxk0o.
//      ,OWW0xk0KKKKK0kd00k;                    ,:.'dl.      oWMKxkKKK0kooxOx,
//       .dNMNOxdxOKKKKOdkXo                 .:kKOxXNd....  ,0WNxkK0KOxdxOx;
//         ;OWWXxloOKKKK0xk0;          .';cdOXWXOKNW0OKK00OkO0KOxxkkkkkOx;
//          .:OWWKOkk0KKK0xk0;   .':lxOXNMMMMMMWWMMMWMMMMMMMMMMMWXKOxxd,
//            .:kNMN0kkOKK0dO0:;x0XWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWN0d:'.
//               ,dKWNKOkkkdxNNWN00XWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWXkdo;
//                 .ckNMNKKKNMMWOxXMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWMMMMMMMN00KKO:
//                    'oONWMMMMMN0XMMMMMMMMMMMMMMMMMMMMMMMMMMMMWK0NMMMMMMWWMMMNk,
//                    .cxkXWMMMMWO0MMMMMMMMMMMMMMMMMMMMMMMMMMMMMWNWMMMMMMMXKNMMMXl.
//                   .oNWWMMMMMMWNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNOxXMMMNd.
//                  .oKNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNKXWMMMWx. ....
//                 .o0KWMMMMMMMMMWNNXKKKXXNNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMXkl;,,,;;.
//                 c00NMMMMMMMMWOolcc:::ccclodxkOKNWMMMMMMMMMMMMMMMMMMMMMMMMMMMXxc;;,,,,,,.
//                'OWWMMMMMMMMMNx;,;;;;,;;;::::;;:coxOKXNMMMMMMMMMMMMMMMMMMMMXx:;:;,,,,,,,.
//                cNMMMMMMMMMMMMNd,,,,,,,,,;::::::::;;:cldkKNMMMMMMMMMMMMMMNkc;:::;;;,;:'
//                dMMMMMMMMMMMMMMNo',,,,,;::::::::::::::::;:ldkKNMMMMMMMMNOl;:::::::::::.
//               .xMMMMMMMMMMMMMMMNOkko::::::::::::::::::::::;;:ldk0XWWNOl::::::::::::::,.
//               .xMMMMMMMMMMMMMMMMMMMWO:;::::::::::::::::::::::::;;clol:::::::::::::::::.
//                dWKXMMMMMMMMMMMMMMMWKd::::::::::::::::::::::::::::::::::::::::::;;:;;::,.
//                cX0kXMMMMMMMMMMMWKko:;:::::::::::::::::::::::::::::::::::::::;lxO00Oxl:;.
//                .OWXNMMMMMMMMWKko:;:::::::::::;:cllc:;;::::::::::::::::::::::xNMMMMMMNd;.
//                .xMMMMMMMMWXko:;:::::::::::::ok0XNWNK0kl;::::::::::::::::::;xWMMMXOKWMKl.
//                 lWMMMMMXkoc;:::::::::::::;:kWMMMMMMMMMNd;::::::::::::::::::kMMMM0:dWMWd'.
//                 ,KMMN0dc;::::::::::::::::;xWMMMMXxOWMMMKc;:::::::::;;,;:::;oXMMMWNWMMXl,.
//                 .dXkl:;::::::::::::::::::;xWMMMMKcdWMMMXl;::::::::::;;;;:::;lONMMMMWXo:;.
//                  .:;;;:::::::::::::::::::;c0WMMMMWWMMMWk;:::::::;;:cclooc;;:;:ldxkxdc;:;.
//                   ':,,;:::::::::::::::::::;:d0NMMMMWNKd::::::;;;:oxxkkkOdloc;:::;;;::::,.
//                   .;;;;;;;;;;:::::::::::::::;:lodddol:;:::;:lk0kdoodddodkXMXkoc;;;:::;;::.
//                   .:oolc::;;;::::::::::::::::::::::::::;:lxKWXkOK00Okk0NMMMMMWX0kxdddxkXNk;.
//                  .oNWWNX0xodddolc::;;;;;;;;;;;;;;;:clodkKWMMK; c0Ok0x::kWMMMMMMMMMMMMMMMMXOxc.
//                 'xNMMMMWK0XNMMWNXKK0OkkkxxxxxxkkO0KNWWMMMMMMx. 'kkdx,  .OMMMMMMMMMMMMMMMNNXxo:.
//              .;dkKXNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM0'   ..    '0MMMMMMMMMMMMMMMXOO;..
//              .ok0NKKMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWx'..     .xWMMMMMMMMMMMMMMMMNXo
//              ,k0kKKKWMMWNNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWKkx:. .:OWMMMMMMMMMMMMMMMMMMXOl.
//              ';.cKNMMMMWOkNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNOOxdxXMMMMMMMMMMMMMMMMMMMWl..
//                 :kONMMMMNKNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNK0KKKNMMMMMMMMMMMMMMMMMW00o
//                .;dO0O0XWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNOlc.
//                 .lcl000NMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMXl'.
//                     ;kXXNNWWMMMMMMMMMNNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNx,
//                      .oXXXXK00XNNWWWWX0NMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWXXNXXOl'
//                       ,o:lddxxOKXXXXXNNNNWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMXxol:.
//                             ..,:lxOXXXKXXXNNWWMMMMMMMWNXKXXKXWWNK0OkOO0KWNkl,.
//                                   .,cdxkk0XXXNNKOOOkxxdoodlldddooclxxoooc;.
//                                       .,:lkOkkxlcdkl;locodlcllddcclodlc;..',c:.
//                                       .;cxd::oOkloOxollccc:lllxdcc;.;l;. .:OOdc'..,
//                                     'ldkxoooo:lxddxxxkxdxkxxOOOOk:   .,,,',::cdocxk:.
//                                  .';lollll:oOo;:okKNWWMMMMMMMMMM0l,         ;OOolcdkl'.
//                              .';coddolcoollONx;cllldxxOXWMMMMMMNdc:.       .xWkclocldd,
//                          ..,codddlc;;lldKXNMMx,,;:cllllodkKX00Xxcl;;,.     :XMWOoollll;.
//                       .,:lddoc;,..  'odlxWMMMO::::;;;;:cllcodoolc;;dOd,   :XMNXX0c.,lddo;.
//                   .';cool:,'.      .',;;kMMMM0:;::::::;;;;colllol;;:lxd..lXMMMWk'   .,cddl,
//                .':ll:;'.           ';:;:OMMMMKc;:::::::;;,:dollol;,:::,'dNMMMMNl       'cooc.
//              .,;;'..              .,;:;lXMMMMXl;:;;;;;::cl:;:cc::::,,',xWMMMMMk.         .:oo:.
//                                  .,;::;xWMMMMNo,::cclc;;;;;,,;;;;;:c,;OWMMMMMX;            .;ol;.
//                                  .,;:;c0MMMMMWdcooc:;,',;:::::::::,,';0MMMMMMx.              'cd:.
// SemiSupers
// Website: https://semisupers.com
// Twitter: https://twitter.com/semisupers
//
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '@openzeppelin/contracts/token/ERC721/ERC721.sol';
import '@openzeppelin/contracts/access/Ownable.sol';
import '@openzeppelin/contracts/token/ERC20/IERC20.sol';
import '@openzeppelin/contracts/utils/cryptography/MerkleProof.sol';
import '@openzeppelin/contracts/security/ReentrancyGuard.sol';

contract SemiSupers is ERC721, Ownable, ReentrancyGuard {
    using Strings for uint256;
    uint256 public constant MAX_SUPPLY = 5555;
    uint256 public publicMintStartBlock =
        0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff;
    uint256 public allowlistMintStartBlock =
        0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff;
    string public METADATA_PROVENANCE_HASH = '';
    string public _baseTokenURI;
    address public vault;
    uint256 public price = 0.08 ether;
    uint256 public nextSuperId = 0;
    bytes32 public allowlistMerkleRoot;
    bool private _mintManuallyClosedForever = false;

    constructor(string memory baseURI) ERC721('SemiSupers', 'SEMISUPERS') {
        setBaseURI(baseURI);
    }

    function mintStarted() public view returns (bool) {
        return block.number >= publicMintStartBlock;
    }

    function allowlistMintStarted() public view returns (bool) {
        return block.number >= allowlistMintStartBlock;
    }

    function mint(uint256 numSupers) public payable nonReentrant {
        require(mintStarted(), 'Mint not started');
        _mintSupers(numSupers);
    }

    function allowlistMint(
        uint256 numSupers,
        bytes32 _leaf,
        bytes32[] calldata _merkleProof
    ) public payable nonReentrant {
        require(allowlistMintStarted(), 'Mint not started');

        // verify allowlist
        bytes32 node = keccak256(abi.encodePacked(msg.sender));
        require(node == _leaf, 'Leaf not matching the node');
        require(
            MerkleProof.verify(_merkleProof, allowlistMerkleRoot, _leaf),
            'Invalid proof.'
        );

        _mintSupers(numSupers);
    }

    function allowlisted(bytes32 _leaf, bytes32[] calldata _merkleProof)
        external
        view
        returns (bool)
    {
        bytes32 node = keccak256(abi.encodePacked(msg.sender));
        if (node != _leaf) return false;
        if (!MerkleProof.verify(_merkleProof, allowlistMerkleRoot, _leaf))
            return false;
        return true;
    }

    function _mintSupers(uint256 numSupers) private {
        require(!_mintManuallyClosedForever, 'Mint is over');
        require(nextSuperId < MAX_SUPPLY, 'All Supers have been minted');
        require(numSupers > 0 && numSupers <= 20, 'Mint from 1 to 20 supers');
        require(
            nextSuperId + numSupers <= MAX_SUPPLY,
            "I'm afraid you're trying to mint too many supers"
        );
        require(
            msg.value == price * numSupers,
            'Ether value sent is not accurate'
        );

        for (uint256 i = 0; i < numSupers; i++) {
            uint256 mintIndex = nextSuperId;
            _safeMint(msg.sender, mintIndex);
            nextSuperId += 1;
        }
    }

    function reserve(uint256 numSupers) public onlyOwner {
        require(nextSuperId + numSupers <= 130, 'Exceeded reserved supply');
        require(!allowlistMintStarted(), 'Too late');
        uint256 index;
        for (index = 0; index < numSupers; index++) {
            _safeMint(owner(), nextSuperId);
            nextSuperId += 1;
        }
    }

    function remainingSupply() public view returns (uint256) {
        if (_mintManuallyClosedForever) {
            return 0;
        }
        return MAX_SUPPLY - nextSuperId;
    }

    function totalSupply() public view returns (uint256) {
        return nextSuperId;
    }

    function tokenURI(uint256 tokenId)
        public
        view
        virtual
        override
        returns (string memory)
    {
        require(
            _exists(tokenId),
            'ERC721Metadata: URI query for nonexistent token'
        );

        return string(abi.encodePacked(_baseTokenURI, tokenId.toString()));
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    /*
     * Only the owner can do these things
     */

    function setProvenanceHash(string memory _hash) public onlyOwner {
        METADATA_PROVENANCE_HASH = _hash;
    }

    function setBaseURI(string memory baseURI) public onlyOwner {
        _baseTokenURI = baseURI;
    }

    function setAllowlistMerkleRoot(bytes32 newMerkleRoot) public onlyOwner {
        allowlistMerkleRoot = newMerkleRoot;
    }

    function setPublicMintStartBlock(uint256 _newPublicMintStartBlock)
        public
        onlyOwner
    {
        publicMintStartBlock = _newPublicMintStartBlock;
    }

    function setAllowlistMintStartBlock(uint256 _newAllowlistMintStartBlock)
        public
        onlyOwner
    {
        allowlistMintStartBlock = _newAllowlistMintStartBlock;
    }

    function setPrice(uint256 _newPrice) public onlyOwner {
        require(
            !allowlistMintStarted(),
            'Price cannot be changed once the minting has started'
        );
        price = _newPrice;
    }

    function setVault(address _newVaultAddress) public onlyOwner {
        vault = _newVaultAddress;
    }

    function closeMintForeverYouCantUndoThis(bool _imSure) public onlyOwner {
        require(_imSure, "You don't seem sure about this");
        _mintManuallyClosedForever = true;
    }

    function withdraw(uint256 _amount) public onlyOwner {
        require(address(vault) != address(0), 'no vault');
        require(payable(vault).send(_amount));
    }

    function withdrawAll() public payable onlyOwner {
        require(address(vault) != address(0), 'no vault');
        require(payable(vault).send(address(this).balance));
    }

    function forwardERC20s(IERC20 _token, uint256 _amount) public onlyOwner {
        require(address(vault) != address(0));
        _token.transfer(vault, _amount);
    }

    function renounceOwnership() public override onlyOwner {}
}