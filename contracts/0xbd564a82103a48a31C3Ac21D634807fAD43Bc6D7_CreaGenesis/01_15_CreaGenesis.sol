//SPDX-License-Identifier: Unlicense
/**
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@PYPB#&@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@#!   :^~7JY5GB#&@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@B7.         .:^~7?Y5G#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@#5!.               [email protected]@?!?Y5GB#&@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#P7^            :&@?      .:^~7?Y5#@@&@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@BJ~.         [email protected]@?            ~#@5:^!JP&@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#5!:       [email protected]?^.    .^[email protected]#7      .7#@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@&GJ~.     ~YB&#BGGB#&#P!.         [email protected]@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#57^.    .^~!!~^:              [email protected]@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#PY?77!!~~~~!!~~~~!~~^^..   ^&@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@&#[email protected]@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@&G&@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
**/

/// Crea - Genesis by Crea @ArtbyCrea 
/// Contract by @aurealarcon aurelianoa.eth

pragma solidity ^0.8.9;

import "erc721a/contracts/extensions/ERC721AQueryable.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/finance/PaymentSplitter.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "./MetadataUpdatable.sol";

contract CreaGenesis is ERC721AQueryable, Ownable, ReentrancyGuard, PaymentSplitter, MetadataUpdatable {

    using ECDSA for bytes32;

    bool private isPublicMint = false;
    bool private isAllowListMint = false;
    uint256 public price = 0.1 ether;
    uint256 public allowListPrice = 0.08 ether;
    uint256 public totalMaxSupply = 700;
    uint256 private maxPerWallet = 4;
    uint256 private publicMaxPerWallet = 4;
    uint256 private teamMax = 12;

    /// @dev allowList mapping(address => minted)
    mapping (address => uint256) private walletsMinted;
    /// @dev public mapping(address => minted)
    mapping(address => uint256) private publicWalletsMinted;

    constructor(
        string memory name,
        string memory symbol, 
        address[] memory _payees, 
        uint256[] memory _shares
        ) 
        ERC721A(name, symbol) 
        PaymentSplitter(_payees, _shares) 
        {}
    
    ///ADMINISTRATIVE TOOLS

    /// @dev set Mint State
    /// @param allowList bool
    /// @param publicList bool
    function setMintState(bool allowList, bool publicList) external onlyOwner {
        isAllowListMint = allowList;
        isPublicMint = publicList;
    }

    /// @notice update max per wallet for each state
    /// @dev true 'allow' or  false 'public'
    /// @param mintState bool
    function updateMaxPerwallet(uint256 max, bool mintState) external onlyOwner {
        if(mintState) {
            maxPerWallet = max;
        } else {
            publicMaxPerWallet = max;
        }
    }

    function _startTokenId() internal view virtual override returns (uint256) {
      return 1;
    }

    /// modifiers

    /// @notice checks if the wallet is allow listed recovering from signature
    /// @param allowListed address
    /// @param _signature bytes memory
    modifier onlyAllowlisted(address allowListed, bytes memory _signature) {
        require(isAllowListMint, "AllowList mint is not active");
        require(isAllowListMinted( allowListed, _signature), "Address not in Allowlist");

        _;
    }

    /// Mint multiple ERC721 tokens
    /// @notice this will receive an array to set the seedMetadata per tokenId
    /// @param owner address
    /// @param variants string[]
    function mint(address owner, string[] memory variants) internal {
        uint256 i = 0;
        uint256 tokenId = totalSupply() + 1;
        do {
            require(isValidVariant(variants[i]),"No valid variant provided");
            _mint(owner, 1);
            _setSelectedVariant(tokenId + i, variants[i]);
            unchecked { ++i; }
        } while (i < variants.length);
    }

    /// Mint by team Members
    /// @notice same rules for the mint function
    /// @param variants string[]
    function teamMint(string[] memory variants) external onlyOwner {
        require(totalSupply() + variants.length <= teamMax, "team mint supply exceeded");
    
        mint(msg.sender, variants);
    }

    /// Mint from the Allow list
    /// @param variants string[]
    /// @param signature bytes
    function allowListMint(string[] memory variants, bytes memory signature) external payable 
    onlyAllowlisted(msg.sender, signature) {
        uint256 count = variants.length;

        require(count <= maxPerWallet, "Maximum per transaction");
        require(totalSupply() + count <= totalMaxSupply, "all tokens already minted"); 
        require(count <= maxPerWallet - walletsMinted[msg.sender], "Maximum per wallet exceeded");
        require(msg.value == count * allowListPrice, "wrong eth sent");

        walletsMinted[msg.sender] += count; 
        mint(msg.sender, variants);
    }

    /// public mint
    /// @param variants string[] 
    function publicMint(string[] memory variants) external payable 
    nonReentrant {
        require(isPublicMint, "Public mint is not active");

        uint256 count = variants.length;

        require(count <= publicMaxPerWallet, "Maximum per transaction");
        require(totalSupply() + count <= totalMaxSupply, "all tokens already minted");
        require(count <= publicMaxPerWallet - publicWalletsMinted[msg.sender], "Maximum per wallet exceeded");
        require(msg.value == count * price, "wrong eth sent");

        publicWalletsMinted[msg.sender] += count;
        mint(msg.sender, variants);
    }

    /// Update variant
    /// @notice the holder can update the gender metadata of the given token
    /// @param tokenId uint256
    /// @param variant string
    function updateVariant(uint256 tokenId, string memory variant) external payable {
        require(isMetadataRevealed(), "Metadata not revealed yet");
        require(ownerOf(tokenId) == msg.sender, "you dont own this token");
        require(msg.value == getVariantPrice(variant), "wrong ETH Sent");
        _setSelectedVariant(tokenId,  variant);
    }

    /// get tokenMetadata
    /// @notice it will use the MetadataUpdatable
    /// @param tokenId uint256
    /// @return string
    function tokenURI(uint256 tokenId) public view override returns (string memory) {
        require(_exists(tokenId), "Token does not exist");

        return getTokenURI(tokenId);
    }

    /// get max mints left
    /// @notice get mints left by user depending on the mint state
    /// @param owner address
    /// @return uint256 
    function maxMintLeft(address owner) external view returns (uint256) {
        return isAllowListMint ? maxPerWallet - walletsMinted[owner] : 
        publicMaxPerWallet - publicWalletsMinted[owner];
    }

    /// @notice checks if the wallet is allow listed recovering from signature
    /// @param allowListed address
    /// @param _signature bytes memory
    /// @return bool
    function isAllowListMinted(address allowListed, bytes memory _signature) public view returns(bool) {

        bytes32 messagehash = keccak256(
            abi.encodePacked(address(this), allowListed)
        );
        address signer = messagehash.toEthSignedMessageHash().recover(
            _signature
        );

        return signer == owner();
    }

    /// allowlist mint getter
    /// @return bool
    function isAllowListMintActive() external view returns (bool) {
        return isAllowListMint;
    }

    /// public mint getter
    /// @return bool
    function isPublicMintActive() external view returns (bool) {
        return isPublicMint;
    }
}