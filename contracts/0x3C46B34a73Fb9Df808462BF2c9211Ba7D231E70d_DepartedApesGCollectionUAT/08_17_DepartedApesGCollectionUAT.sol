// SPDX-License-Identifier: MIT
/*                                                                                                                                                                                                                                                        
                                                                                                                                                                                                                    ...::^^~!77?JJJJJJ?~                  
                                                                                                                                                                                                 ....:^^~!7! .JY55PPPGGGGBBBBGRID6GRID6G5                 
                                                                                                                                                                               ...::^~!~ !JJY55PPPGGGBBBBBB? JGRID6GRID6P7!!?GRID6GRID6GY                 
                                                                                                                                                                :^~~!7?JJY55PPPGGGGBBBB?^GRID6GRID6GJ77!~~^ .GRID6GRID6G~   .GRID6GRID6G^                 
                                                                                                                                         ...::^~~!7??JYYYYYY?: YBBBBGRID6GRID6GGGY?7!!~ JGRID6GRID67        7GRID6GRID6P    7GRID6GRID65                  
                                                                                                                      ...:.        JY55PPPGGGGBBBGRID6GRID6GGP.7?7!~?GRID6GRID6GJ      .GRID6GRID6G.        PGRID6GRID67    PGRID6GRID6!                  
                                                                                                            ~7?JYY55PPPGGGP       ~GRID6GRID6G?!~?GRID6GRID6GY      !GRID6GRID6G:      ?GRID6GRID6P^^~!7?: ~GRID6GRID6G.   ~GRID6GRID6P                   
                                                                                ....:^^~!!7?JJYY55YY?~     ?GRID6GRID6GRID6.      5GRID6GRID6J   ^GRID6GRID6G^      PGRID6GRID65       PGRID6GRID6GBBBBBB: 5GRID6GRID6J    5GRID6GRID6?                   
                                                              ...::^~!!7?J.^55PPPGRID6GBBBGRID6GRID6GB7   ?GRID6GRID6GRID6G^     :GRID6GRID6G:   YGRID6GRID6Y      ~GRID6GRID6G~      !GRID6GRID6P?7!~^^. :GRID6GRID6G:   :GRID6GRID6G.                   
                                           ........    ^Y55PPPGRID6BBBBBBP.5GRID6GRID657!!5GRID6GRID6G!  ?GRID6GGPPGRID6GGG!     JGRID6GRID65   !GRID6GGGG5!       5GRID6GRID6P       5GRID6GRID6~        JGRID6GRID65    JGRID6GRID6Y                    
                           ::^^~!7??JY55PPPGRID6GGGP5: PGRID6GRID6Y!~~^:: ^GRID6GRID6G:   7GRID6GRID6P  ?GRID6GGP~PGRID6GGG?    .GRID6GRID6GP55PGRID6GGPJ^        :GRID6GRID6G7      ^GRID6GRID6P        .GRID6GRID6G~   .GRID6GRID6G:                    
                          YGRID6GGGGYGRID6YGRID6GRID67^GRID6GRID6P        YGRID6GRID6Y    PGRID6GRID6? ?GRID6GGG: GRID6GGGGY    7GRID6GRID6GRID6GRID6GGGPPJ       JGRID6GRID6G.      YGRID6GRID67        7GRID6GRID6P:.:~5GRID6GGGGP~                     
                         .GRID6GRID6G!   !GRID6GRID6G:YGRID6GRID6!       :GRID6GRID6G^   ^GRID6GRID6G:?GRID6GGG! .GRID6GGGG5    PGRID6GRID6Y^^^JGRID6GRID6G.     .GRID6GRID6GJ      .GRID6GRID6G^...:^^  PGRID6GRID6GGGGBGRID6GP5J~.                      
                         ?GRID6GRID6P    ?GRID6GRID6Y:GRID6GRID6G?~!7?J? ?GRID6GRID6P   :5GRID6GRID67?GRID6GGG7  .GRID6GGGGP.  ~GRID6GRID6G.   ?GRID6GRID6Y      7GRID6GRID6G:      ?GRID6GRID6GRID6GBP ~BGRID6GPP55YYJ??7!~^:.                           
                        .PGRID6GRID6!   .PGRID6GRID6^?GRID6GRID6GBBBBGB7.PGRID6GRID6PYY5GRID6GRID6J^?GRID6GGGP~^~?GRID6GRID6:  5GRID6GRID6J   .PGRID6GRID6^      PGRID6GRID65      .GRID6GGGPPP55YYJ??: :~^::...                                          
                        !GRID6GRID6P.   !GRID6GRID65:PGRID6GRID6?~~^::. !GRID6GRID6GRID6PP55YJ7~:. ?GRID6GRID6BBBGRID6GRID6G^ ^GRID6GRID6G:   !GRID6GRID65      !BGRID6GPPP5^      .!~^^::...                                                             
                        5GRID6GRID6?    PGRID6GRID6!!GRID6GRID65        PGRID6GRID6?:...          ?GRID6GGGPJ?7!~7GRID6GRID6! JGRID6GRID65    PGRID6GPPPP~      :~^^:....                                                                                 
                       ^GRID6GRID6G:   ^GRID6GRID6P:5GRID6GRID6~       ^GRID6GRID6P              ?GRID6GGGG^     .GRID6GGGGBJ.GGGGPPP55YY:    ~^^::...                                                                                               
                       YGRID6GRID6Y    YGRID6GRID6?^GRID6GRID65        YGRID6GRID67             JGRID6GGGG!      .555YJ?7!!~: :.....                                                                                         
                      .GRID6GRID6G^   :GRID6GRID6G:JGRID6GRID6Y.::^~!::GRID6GRID6G.            7PYYJ?77!~:              .:^~!77?JYY555YYJ?~                                                          
                      ?GRID6GRID65    JGRID6GRID6?:GRID6GRID6GGGGBBBB^?GGGPPP55YY!             .....::^^~!7?JJ5:   .!J5PGRID6BBGRID6GRID6GG5                                                          
                     .PGRID6GRID6G2022GRID6GGGG5~ JBGGGGPPP55YY??7!~^ :::.........:::::.    ~5PPPPGRID6BBBBGGGP  ^5GRID6GGGGY^:^PGRID6GRID6P                                                         
                     !GRID6GRID6GGBGGGPP5YJ7~:.   :^::...      :^^~~!7??JY55PPPGRID6GGGGP~  PGRID6GRID6?^^::..  ?GRID6GRID65    PGRID6GRID67                                                  
                     YP5YYJ?77!~^^::... ....::^~!77??Y^       ~GRID6GRID6GGPPGRID6GRID6GG5 ~GRID6GRID65        .GRID6GRID6G^   :YJ??7!!~^^:                                                           
                                       !PGRID6GRID6GGGJ       5GRID6GRID6Y.  ^GRID6GRID6G7 5GRID6GRID6!        ?GRID6GRID6P.  ...::::..                                                              
                                      7GRID6GRID6GRID65      :GRID6GRID6G:   ~GRID6GRID6G.:GRID6GRID6GJ7?JY5J  PGRID6GRID6GPLFGMGRID6GGP5:                                                       
                                     7GRID6GRID6GRID6GP      JGRID6GRID65    5GRID6GRID6J JGRID6GRID6GGBGGGG!  !PGRID6GGGPPP55GRID6GRID6GY                                                          
                                    7GRID6GGPYGRID6GGGG.    .GRID6GRID6G^   ^GRID6GRID6G..GRID6GRID6G7^::...      ........   :GRID6GRID6G~                                                                   
                                   7GRID6GGG:!GRID6GGGG:    7GRID6GRID6G^:^7PGRID6GGGGP: 7GRID6GRID6Y         .::^~~!77?J.   7GRID6GRID6P                                                                         
                                  7GRID6GGG! !GRID6GGGG~    PGRID6GRID6GGGBBGRID6GP5J~   PGRID6GRID6^        YGRID6GRID65   .PGRID6GRID6^                                                                                 
                                 7GRID6GGG?  !GRID6GGGG7   ~GRID6GRID6GYJJ?7!!~^:.      ~GRID6GRID65        .GRID6GRID6G5^^!5GRID6GGGP7.                                                                                                                  
                                7GRID6GGG5   ?GRID6GGGGY   5GRID6GRID67                 5GRID6GRID6Y:^~!7?^  JGRID6GRID6GBBBGGGP5Y?~:                                                                                                                     
                               7GRCODEBYSOLDEVWITHLOVEG5  ^GRID6GRID6G.                :GRID6GRID6GGBBBBBB:   .^!7?????7!!~^::.                                                                                                                           
                              7GRID6GRID6GPPPPGRID6GGGGP  JGRID6GRID6?                 JGGGPPP55YYJ?77!~^:                                                                                                                                                
                             7GRID6GGGG~..   7GRID6GRID6..GRID6GRID6G:                 ::...                                                                                                                                                              
                            7GRID6GGGG!      7BGRID6GPPP:~5YJ?77!~~^.                                                                                                                                                                                     
                           ?BGRID6PPP7       .~^::...                                                                                                                                                                                                     
                          .~^::...                                                                                                                                                                                                                                                                                                                                                                                                                                                                             
*/
pragma solidity ^0.8.14;

import "@openzeppelin/contracts-upgradeable/security/ReentrancyGuardUpgradeable.sol";
import 'erc721a-upgradeable/contracts/ERC721AUpgradeable.sol';
import 'erc721a-upgradeable/contracts/IERC721AUpgradeable.sol';
import '@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol';
import "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";
import "@openzeppelin/contracts-upgradeable/security/PausableUpgradeable.sol";
import {RevokableDefaultOperatorFiltererUpgradeable} from "./opensea/upgradeable/RevokableDefaultOperatorFiltererUpgradeable.sol";
import {RevokableOperatorFiltererUpgradeable} from "./opensea/upgradeable/RevokableOperatorFiltererUpgradeable.sol";

contract DepartedApesGCollectionUAT is 
    ERC721AUpgradeable, 
    ReentrancyGuardUpgradeable, 
    PausableUpgradeable,
    RevokableDefaultOperatorFiltererUpgradeable,
    OwnableUpgradeable
{
    string public baseURI; 
    string public tokenURISuffix;
    uint256 public constant MAX_SUPPLY = 10000;    
    uint256 public constant MAX_PER_FC = 2;
    uint256 public constant MAX_PER_ADDRESS_WL = 2;
    uint256 public constant MAX_PER_ADDRESS_PUB = 1;

    uint256 public constant mintStart = 1670838249;
    uint256 public constant fortuneCookiesEnd = 1670925600;
    uint256 public constant waitlistEnd = 1670947200;
    uint256 public constant mintEnd = 1670990400;
    uint256 public constant fortuneCookiesPrice = 0.00001 ether;
    uint256 public constant waitlistPrice = 0.00001 ether;
    uint256 public constant publicPrice = 0.00002 ether;
    uint256 public totalMinted;

    address public constant FC_NFT_PROXY = 0x25e83E339B5414909CDE81F7BF0A5401B21201F9;

    bytes32 public merkleRoot;

    mapping(address => uint256) mintedAccountsB4Pub;
    mapping(address => uint256) mintedAccountsPUB;

    struct MintQuota {        
        uint256 leftQuota;
        uint256 maxQuota;
        uint256 currentPrice;
    }

    function initialize(        
        string memory _coverBaseURI,
        string memory _tokenURISuffix,
        bytes32 _merkleRoot
    ) initializerERC721A initializer public {
        __ERC721A_init('DepartedApesGCollectionUAT', 'DAGCUAT');
        __Ownable_init();
        __RevokableDefaultOperatorFilterer_init();

        baseURI = _coverBaseURI;
        tokenURISuffix = _tokenURISuffix;
        totalMinted = 0;
        merkleRoot = _merkleRoot;
    }

    // utility
    function setNewMerkleRoot(bytes32 _newRoot) external onlyOwner {
        merkleRoot = _newRoot;
    }

    function _merkleTreeLeaf(address _address) internal pure returns (bytes32) {
        return keccak256((abi.encodePacked(_address)));
    }
    
    function _merkleTreeVerify(bytes32 _leaf, bytes32[] memory _proof) internal view returns(bool) {
        return MerkleProof.verify(_proof, merkleRoot, _leaf);
    }

    function _isFortuneCookiesHolder(address _user) internal view returns(bool) {
        IERC721AUpgradeable fcContract = IERC721AUpgradeable(FC_NFT_PROXY);
        return fcContract.balanceOf(_user) == 1;
    }

    // Mint status query
    function getMintStatus(address _user) public view returns (MintQuota memory) {
        if (block.timestamp <= fortuneCookiesEnd) {
            return MintQuota(
                MAX_PER_FC - mintedAccountsB4Pub[_user],
                MAX_PER_FC,
                fortuneCookiesPrice
            );
        } else if (block.timestamp <= waitlistEnd) {
            return MintQuota(
                MAX_PER_ADDRESS_WL - mintedAccountsB4Pub[_user],
                MAX_PER_ADDRESS_WL,
                waitlistPrice
            );
        } else {
            return MintQuota(
                MAX_PER_ADDRESS_PUB - mintedAccountsPUB[_user],
                MAX_PER_ADDRESS_PUB,
                publicPrice
            );
        }
    }

    // Team mint
    function devMint(address _to, uint256 _quantity) external onlyOwner {
        require(_quantity + totalMinted <= MAX_SUPPLY);
        _mintBatch(_to, _quantity);
    }

    function _mintBatch(address _to, uint256 _quantity) virtual internal {
        require(_quantity > 0, "Quantity must be greater than 0");
        _safeMint(_to, _quantity);   
        totalMinted += _quantity;
    }

    // Mint
    function mint(uint256 _quantity, bytes32[] calldata proof) external nonReentrant payable whenNotPaused {
        require(
            (mintStart <= block.timestamp && mintEnd > block.timestamp), 
            "Mint is not active."
        );        
        require(
            totalMinted + _quantity <= MAX_SUPPLY,
            "SOLD OUT!"
        );  

        if(block.timestamp <= fortuneCookiesEnd) {
            // fortune cookies round
            require(
                _isFortuneCookiesHolder(msg.sender),
                "Sorry, you don't own any fortune cookies, please come back later."
            );
            require(
                mintedAccountsB4Pub[msg.sender] + _quantity <= MAX_PER_FC,
                "Sorry, you have minted all your quota in non-public round."
            ); 
            require(
                msg.value == fortuneCookiesPrice * _quantity,
                "Insufficient payment."
            );
            _mintBatch(msg.sender, _quantity);      
            mintedAccountsB4Pub[msg.sender] += _quantity;   

        } else if (block.timestamp <= waitlistEnd) {
            // waitlist round
            require(_merkleTreeVerify(_merkleTreeLeaf(msg.sender), proof),
                "Sorry, you are not in this waitlist, please come back later at public round."
            );
            require(
                mintedAccountsB4Pub[msg.sender] + _quantity <= MAX_PER_ADDRESS_WL,
                "Sorry, you have minted all your quota in non-public round."
            ); 
            require(
                msg.value == waitlistPrice * _quantity,
                "Insufficient payment."
            );
            _mintBatch(msg.sender, _quantity);      
            mintedAccountsB4Pub[msg.sender] += _quantity; 
        } else {
            // Public tier
            require(
                mintedAccountsPUB[msg.sender] + _quantity <= MAX_PER_ADDRESS_PUB,
                "Sorry, you have minted all your quota in public round."
            );
            require(
                msg.value == publicPrice * _quantity,
                "Insufficient payment."
            );            
            _mintBatch(msg.sender, _quantity);       
            mintedAccountsPUB[msg.sender] += _quantity;  
        }  
    }

    // Post Mint
    function _baseURI() internal view virtual override returns (string memory) {
        return baseURI;
    }

    function setBaseURI(string memory _newBaseURI) external onlyOwner {
        baseURI = _newBaseURI;
    }

    function setTokenURISuffix(string memory _newTokenURISuffix) external onlyOwner {
        tokenURISuffix = _newTokenURISuffix;
    }
    function tokenURI(uint256 _tokenId) public view override returns(string memory) {
        return string.concat(super.tokenURI(_tokenId), tokenURISuffix);
    }

    // Fund Withdraw
    function withdrawETH(address _to) external onlyOwner {
        require(_to != address(0), "Cant transfer to 0 address!");
        (bool withdrawSucceed, ) = payable(_to).call{ value: address(this).balance }("");
        require(withdrawSucceed, "Withdraw Failed");
    }

    // Admin pause
    function pause() external onlyOwner {
        _pause();
    }

    function unpause() external onlyOwner {
        _unpause();
    }

    // Opensea Operator filter registry
    function setApprovalForAll(address operator, bool approved) public override onlyAllowedOperatorApproval(operator) {
        super.setApprovalForAll(operator, approved);
    }

    function approve(address operator, uint256 tokenId) public override onlyAllowedOperatorApproval(operator) {
        super.approve(operator, tokenId);
    }

    function transferFrom(address from, address to, uint256 tokenId) public override onlyAllowedOperator(from) {
        super.transferFrom(from, to, tokenId);
    }

    function safeTransferFrom(address from, address to, uint256 tokenId) public override onlyAllowedOperator(from) {
        super.safeTransferFrom(from, to, tokenId);
    }

    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory data)
        public
        override
        onlyAllowedOperator(from)
    {
        super.safeTransferFrom(from, to, tokenId, data);
    }

    function owner() public view virtual override (OwnableUpgradeable, RevokableOperatorFiltererUpgradeable) returns (address) {
        return OwnableUpgradeable.owner();
    }
}