// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;

import "@solv/v2-solidity-utils/contracts/openzeppelin/token/ERC721/ERC721Upgradeable.sol";
import "@solv/v2-solidity-utils/contracts/openzeppelin/utils/EnumerableSetUpgradeable.sol";
import "./interface/IVNFT.sol";
import "./interface/optional/IVNFTMetadata.sol";

abstract contract VNFTCoreV2 is IVNFT, IVNFTMetadata, ERC721Upgradeable {
    using SafeMathUpgradeable for uint256;
    using AddressUpgradeable for address;
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.UintSet;

    struct ApproveUnits {
        address[] approvals;
        mapping(address => uint256) allowances;
    }

    /// @dev tokenId => units
    mapping(uint256 => uint256) internal _units;

    /// @dev tokenId => operator => units
    mapping(uint256 => ApproveUnits) private _tokenApprovalUnits;

    /// @dev slot => tokenIds
    mapping(uint256 => EnumerableSetUpgradeable.UintSet) private _slotTokens;

    uint8 internal _unitDecimals;

    function _initialize(
        string memory name_,
        string memory symbol_,
        uint8 unitDecimals_
    ) internal virtual {
        ERC721Upgradeable.__ERC721_init(name_, symbol_);
        ERC165Upgradeable._registerInterface(type(IVNFT).interfaceId);
        _unitDecimals = unitDecimals_;
    }

    function _safeTransferUnitsFrom(
        address from_,
        address to_,
        uint256 tokenId_,
        uint256 targetTokenId_,
        uint256 transferUnits_,
        bytes memory data_
    ) internal virtual {
        _transferUnitsFrom(
            from_,
            to_,
            tokenId_,
            targetTokenId_,
            transferUnits_
        );
        require(
            _checkOnVNFTReceived(
                from_,
                to_,
                targetTokenId_,
                transferUnits_,
                data_
            ),
            "to non VNFTReceiver implementer"
        );
    }

    function _transferUnitsFrom(
        address from_,
        address to_,
        uint256 tokenId_,
        uint256 targetTokenId_,
        uint256 transferUnits_
    ) internal virtual {
        require(from_ == ownerOf(tokenId_), "source token owner mismatch");
        require(to_ != address(0), "transfer to the zero address");

        _beforeTransferUnits(
            from_,
            to_,
            tokenId_,
            targetTokenId_,
            transferUnits_
        );

        // approve all后可不需要approve units
        if (_msgSender() != from_ && !isApprovedForAll(from_, _msgSender())) {
            _tokenApprovalUnits[tokenId_].allowances[
                _msgSender()
            ] = _tokenApprovalUnits[tokenId_].allowances[_msgSender()].sub(
                transferUnits_,
                "transfer units exceeds allowance"
            );
        }

        _units[tokenId_] = _units[tokenId_].sub(
            transferUnits_,
            "transfer excess units"
        );

        if (!_exists(targetTokenId_)) {
            _mintUnits(to_, targetTokenId_, _slotOf(tokenId_), transferUnits_);
        } else {
            require(
                ownerOf(targetTokenId_) == to_,
                "target token owner mismatch"
            );
            require(
                _slotOf(tokenId_) == _slotOf(targetTokenId_),
                "slot mismatch"
            );
            _units[targetTokenId_] = _units[targetTokenId_].add(transferUnits_);
        }

        emit TransferUnits(
            from_,
            to_,
            tokenId_,
            targetTokenId_,
            transferUnits_
        );
    }

    function _merge(uint256 tokenId_, uint256 targetTokenId_) internal virtual {
        require(
            _isApprovedOrOwner(_msgSender(), tokenId_),
            "VNFT: not owner nor approved"
        );
        require(tokenId_ != targetTokenId_, "self merge not allowed");
        require(_slotOf(tokenId_) == _slotOf(targetTokenId_), "slot mismatch");

        address owner = ownerOf(tokenId_);
        require(owner == ownerOf(targetTokenId_), "not same owner");

        uint256 mergeUnits = _units[tokenId_];
        _units[targetTokenId_] = _units[tokenId_].add(_units[targetTokenId_]);
        _burn(tokenId_);

        emit Merge(owner, tokenId_, targetTokenId_, mergeUnits);
    }

    function _split(
        uint256 tokenId_,
        uint256 newTokenId_,
        uint256 splitUnits_
    ) internal virtual {
        require(
            _isApprovedOrOwner(_msgSender(), tokenId_),
            "VNFT: not owner nor approved"
        );
        require(!_exists(newTokenId_), "new token already exists");

        _units[tokenId_] = _units[tokenId_].sub(splitUnits_);

        address owner = ownerOf(tokenId_);
        _mintUnits(owner, newTokenId_, _slotOf(tokenId_), splitUnits_);

        emit Split(owner, tokenId_, newTokenId_, splitUnits_);
    }

    function _mintUnits(
        address minter_,
        uint256 tokenId_,
        uint256 slot_,
        uint256 units_
    ) internal virtual {
        if (!_exists(tokenId_)) {
            ERC721Upgradeable._mint(minter_, tokenId_);
            _slotTokens[slot_].add(tokenId_);
        }

        _units[tokenId_] = _units[tokenId_].add(units_);
        emit TransferUnits(address(0), minter_, 0, tokenId_, units_);
    }

    function _burn(uint256 tokenId_) internal virtual override {
        address owner = ownerOf(tokenId_);
        uint256 slot = _slotOf(tokenId_);
        uint256 burnUnits = _units[tokenId_];

        _slotTokens[slot].remove(tokenId_);
        delete _units[tokenId_];

        ERC721Upgradeable._burn(tokenId_);
        emit TransferUnits(owner, address(0), tokenId_, 0, burnUnits);
    }

    function _burnUnits(uint256 tokenId_, uint256 burnUnits_)
        internal
        virtual
        returns (uint256 balance)
    {
        address owner = ownerOf(tokenId_);
        _units[tokenId_] = _units[tokenId_].sub(
            burnUnits_,
            "burn excess units"
        );

        emit TransferUnits(owner, address(0), tokenId_, 0, burnUnits_);
        return _units[tokenId_];
    }

    function approve(
        address to_,
        uint256 tokenId_,
        uint256 allowance_
    ) public virtual override {
        require(_msgSender() == ownerOf(tokenId_), "VNFT: only owner");
        _approveUnits(to_, tokenId_, allowance_);
    }

    function allowance(uint256 tokenId_, address spender_)
        public
        view
        virtual
        override
        returns (uint256)
    {
        return _tokenApprovalUnits[tokenId_].allowances[spender_];
    }

    /**
     * @dev Approve `to_` to operate on `tokenId_` within range of `allowance_`
     */
    function _approveUnits(
        address to_,
        uint256 tokenId_,
        uint256 allowance_
    ) internal virtual {
        if (_tokenApprovalUnits[tokenId_].allowances[to_] == 0) {
            _tokenApprovalUnits[tokenId_].approvals.push(to_);
        }
        _tokenApprovalUnits[tokenId_].allowances[to_] = allowance_;
        emit ApprovalUnits(to_, tokenId_, allowance_);
    }

    /**
     * @dev Clear existing approveUnits for `tokenId_`, including approved addresses and their approved units.
     */
    function _clearApproveUnits(uint256 tokenId_) internal virtual {
        ApproveUnits storage approveUnits = _tokenApprovalUnits[tokenId_];
        for (uint256 i = 0; i < approveUnits.approvals.length; i++) {
            delete approveUnits.allowances[approveUnits.approvals[i]];
            delete approveUnits.approvals[i];
        }
    }

    function unitDecimals() public view override returns (uint8) {
        return _unitDecimals;
    }

    function unitsInSlot(uint256 slot_)
        public
        view
        override
        returns (uint256 units_)
    {
        for (uint256 i = 0; i < tokensInSlot(slot_); i++) {
            units_ = units_.add(unitsInToken(tokenOfSlotByIndex(slot_, i)));
        }
    }

    function unitsInToken(uint256 tokenId_)
        public
        view
        virtual
        override
        returns (uint256)
    {
        return _units[tokenId_];
    }

    function tokensInSlot(uint256 slot_)
        public
        view
        override
        returns (uint256)
    {
        return _slotTokens[slot_].length();
    }

    function tokenOfSlotByIndex(uint256 slot_, uint256 index_)
        public
        view
        override
        returns (uint256)
    {
        return _slotTokens[slot_].at(index_);
    }

    function slotOf(uint256 tokenId_) public view override returns (uint256) {
        return _slotOf(tokenId_);
    }

    function _slotOf(uint256 tokenId_) internal view virtual returns (uint256);

    /**
     * @dev Before transferring or burning a token, the existing approveUnits should be cleared.
     */
    function _beforeTokenTransfer(
        address from_,
        address to_,
        uint256 tokenId_
    ) internal virtual override {
        if (from_ != address(0)) {
            _clearApproveUnits(tokenId_);
        }
    }

    function _beforeTransferUnits(
        address from_,
        address to_,
        uint256 tokenId_,
        uint256 targetTokenId_,
        uint256 transferUnits_
    ) internal virtual {}

    function _checkOnVNFTReceived(
        address from_,
        address to_,
        uint256 tokenId_,
        uint256 units_,
        bytes memory _data
    ) internal returns (bool) {
        if (!to_.isContract()) {
            return true;
        }
        bytes memory returndata = to_.functionCall(
            abi.encodeWithSelector(
                IVNFTReceiver(to_).onVNFTReceived.selector,
                _msgSender(),
                from_,
                tokenId_,
                units_,
                _data
            ),
            "non VNFTReceiver implementer"
        );
        bytes4 retval = abi.decode(returndata, (bytes4));
        /*b382cdcd  =>  onVNFTReceived(address,address,uint256,uint256,bytes)*/
        return (retval == type(IVNFTReceiver).interfaceId);
    }
}