// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@,,@@@@@,,,,@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@,,     ,,,,     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@   (@@       ,,,,           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@            ,,,,,,           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@((((.....,,,,,,,,...        (((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@,,,,,,,,,,,,,,,,           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@##,,,,,,.....,,,,,,,,,,,           ((((@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@,,,,,,,,     ,,,,,,,,,,,,,,,           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@,,,,,,,,...  ,,,,,,,,,,,,,,,           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@,,,,,,,,,,,,,,, ,,,,,,,,,,,,,,,             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@/////****,,,,,,,,,,,,,,  ,,,,,,,,           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@////   ////,,,,,,,,,,,,    ,,,,,,,,,,,                  @@@@@@@@@@@@@@@@@               @@@@@@@@@@@       @@@@@@@@@@@@
// @@////// //////,,,,,...   ,,,,,,,,,,,,,,           ,,,,,,                           ,,,,,,     ((               @@@@@@@@
// @@@@////////////////@@@@  ,,,,,,,,,,,,,,,,       ,,,,,,,,,,,,,,,,,,,,,         ,,,,,,,,,,,,,,,,   @@@@@           @@@@@@
// @@@@%%%///////////%%@@@@  ,,,,,,,,,,,,,,,,......,,,,,,,,,,,,,,,,,,,,,,...  ....,,,,,,,,,,,,,,,,..((((@@           (((@@@
// @@@@@@@@@@@@@@@@@@@@@@@@  ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,    @@@@@           @@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@   ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,    @@@@@           @@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@    ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,    @@@@@           @@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@  ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,    @@@@@           @@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@   ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,    @@           @@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,  @@@@@@         @@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,  ,,,,,,,,,,,,,,,,,,,,,,,,,  @@@@@@         @@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  ,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,   ,,,,,,,,,,,,,,,,,,,   @@@@@@@@         @@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     ,,,,,,,,,,,,,    ,,,,,,,,,,,,,     ,,,,,,,,,,,,,,,,,   @@@@@@@@@@@           @@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       ,,,,,,,,,  @@@@             @@@@@,,,,,,,,,,,,,,,       @@@@@@@@@@@      @@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     @@,,,,,,,,,  @@@@@@@@@@@@@@@@@@@@@@,,,,,,,,,,,,,,,           @@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     @@####,,,,,[email protected]@@@@@@@@@@@@@@@@@@@@@####,,,,,,,,,,,           @@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     @@@@@@,,,,,,,@@@@@@@@@@@@@@@@@@@@@@@@@@,,,,,,,,,,,           @@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     ((@@@@@@##,,,((@@@@@@@@@@@@@@@@@@@@@@@@@@@@,,,[email protected]@(((((      @@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     @@@@@@@@@@,,,@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@,,,    @@@@@@@      @@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(   @@@@@@@@@@@@@,,,@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#,,,@@@@@@@@@      @@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@,,,,,@@@@@@@@@@@@@@@@@@@@@@@@@@,,,,,  @@@@@@         @@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@//,,,,    @@@@@@@/////,,  @@@@@@@@@@@@@@@@@@@@//////,,,@@@@@@//,,,  @@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@//////,,@@@@@@@@@///////@@@@@@@@@@@@@@@@@@@@@@/////////@@@@@@/////,,@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//
// Forgotten Runes Elysian Fields
// Website: https://forgottenrunes.com
// Twitter: https://twitter.com/forgottenrunes
//
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import '@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol';
import '@openzeppelin/contracts/access/Ownable.sol';
import '@openzeppelin/contracts/token/ERC20/IERC20.sol';
import '@openzeppelin/contracts/utils/cryptography/MerkleProof.sol';
import '@openzeppelin/contracts/security/ReentrancyGuard.sol';
import '@openzeppelin/contracts/utils/cryptography/ECDSA.sol';
import '@openzeppelin/contracts/utils/cryptography/draft-EIP712.sol';

contract ElysianFields is ERC721Enumerable, Ownable, ReentrancyGuard, EIP712 {
    using Strings for uint256;
    uint256 public constant MAX_SUPPLY = 567;

    string public constant R =
        'Put thy Rune on the Gate, and enter the Elysian Fields.';

    uint256 public startBlock =
        0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff;
    string public METADATA_PROVENANCE_HASH = '';
    address public codeSigner;

    string public _baseTokenURI;
    uint256 public nextId = 0;
    bytes32 public allowlistMerkleRoot;

    mapping(bytes32 => bool) public usedCodes;
    mapping(address => bool) public allowlistMinted;
    mapping(uint256 => uint256) public genes;

    constructor(string memory baseURI)
        EIP712('ElysianFields', '1')
        ERC721('ElysianFields', 'PONIES')
    {
        setBaseURI(baseURI);
    }

    function mintStarted() public view returns (bool) {
        return block.number >= startBlock;
    }

    function codeSummon(bytes memory signature, bytes32 code)
        public
        nonReentrant
    {
        require(mintStarted(), 'Mint not started');

        // construct an expected hash, given the parameters
        bytes32 digest = _hashTypedDataV4(
            keccak256(
                abi.encode(
                    keccak256('CodeSummon(address minter,bytes32 code)'),
                    msg.sender,
                    code
                )
            )
        );
        // now recover the signer from the provided signature
        address signer = ECDSA.recover(digest, signature);

        // make sure the recover extracted a signer, but beware, because this
        // can return non-zero for some invalid cases (apparently?)
        require(signer != address(0), 'ECDSA: invalid signature');
        require(signer == codeSigner, 'Signature not by signer');

        // verify this code hasn't already been used
        require(usedCodes[code] == false, 'Code already used');
        usedCodes[code] = true;

        _mintPony();
    }

    function allowlistSummon(bytes32 _leaf, bytes32[] calldata _merkleProof)
        public
        nonReentrant
    {
        require(mintStarted(), 'Mint not started');

        // verify didn't already mint
        require(allowlistMinted[msg.sender] == false, 'Dont be greedy');
        allowlistMinted[msg.sender] = true;

        // verify allowlist
        bytes32 node = keccak256(abi.encodePacked(msg.sender));
        require(node == _leaf, 'Leaf not matching the node');
        require(
            MerkleProof.verify(_merkleProof, allowlistMerkleRoot, _leaf),
            'Invalid proof.'
        );

        _mintPony();
    }

    function _mintPony() private {
        require(nextId < MAX_SUPPLY, 'All Ponies have been summoned');
        _safeMint(msg.sender, nextId);
        genes[nextId] = pseudorandom(nextId);
        nextId += 1;
    }

    function allowlisted(
        address who,
        bytes32 _leaf,
        bytes32[] calldata _merkleProof
    ) external view returns (bool) {
        bytes32 node = keccak256(abi.encodePacked(who));
        if (node != _leaf) return false;
        if (!MerkleProof.verify(_merkleProof, allowlistMerkleRoot, _leaf))
            return false;
        return true;
    }

    // You wouldn't let a pony near an open flame, would you?
    function burn(uint256 tokenId) public virtual {
        require(
            _isApprovedOrOwner(_msgSender(), tokenId),
            'ERC721Burnable: caller is not owner nor approved'
        );
        _burn(tokenId);
    }

    function remainingSupply() public view returns (uint256) {
        return MAX_SUPPLY - nextId;
    }

    function tokenURI(uint256 tokenId)
        public
        view
        virtual
        override
        returns (string memory)
    {
        require(
            _exists(tokenId),
            'ERC721Metadata: URI query for nonexistent token'
        );

        return string(abi.encodePacked(_baseTokenURI, tokenId.toString()));
    }

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    // Anon, have you considered,
    // front-running,
    // your own life?
    function pseudorandom(uint256 seed) internal view returns (uint256) {
        return
            uint256(
                keccak256(
                    abi.encodePacked(
                        tx.origin,
                        blockhash(block.number - 1),
                        block.timestamp,
                        seed
                    )
                )
            );
    }

    /*
     * Only the owner can do these things
     */

    function setProvenanceHash(string memory _hash) public onlyOwner {
        METADATA_PROVENANCE_HASH = _hash;
    }

    function setBaseURI(string memory baseURI) public onlyOwner {
        _baseTokenURI = baseURI;
    }

    function setAllowlistMerkleRoot(bytes32 newMerkleRoot) public onlyOwner {
        allowlistMerkleRoot = newMerkleRoot;
    }

    function setMintStartBlock(uint256 _newMintStartBlock) public onlyOwner {
        startBlock = _newMintStartBlock;
    }

    function setCodeSigner(address newCodeSigner) public onlyOwner {
        codeSigner = newCodeSigner;
    }

    function domainSeparator() external view returns (bytes32) {
        return _domainSeparatorV4();
    }

    function withdrawAll() public payable onlyOwner {
        require(payable(msg.sender).send(address(this).balance));
    }

    function forwardERC20s(IERC20 _token, uint256 _amount) public onlyOwner {
        require(address(msg.sender) != address(0));
        _token.transfer(msg.sender, _amount);
    }

    function renounceOwnership() public override onlyOwner {}

    function uploadImage(bytes calldata s) external onlyOwner {}

    function uploadAttributes(bytes calldata s) external onlyOwner {}
}